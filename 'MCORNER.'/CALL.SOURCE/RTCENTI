      *****************************************************************
      *                 IDENTIFICATION     DIVISION                   *
      *****************************************************************
       IDENTIFICATION DIVISION.
      *
       PROGRAM-ID.      RTCENTI.
       AUTHOR.          MIGUEL DE LA OSSA
      *
      *****************************************************************
      **  PROYECTO .........: HOL                                    **
      **  PROGRAMA .........: RTCENTI                                **
      **  VERSION ..........: 1.0                                    **
      **  TITULO ...........: RUTINA DE CENTRADO DE TEXTOS           **
      **                                                             **
      **  TIPO .............:                                        **
      **     - LENGUAJE ...............: COBOL II                    **
      **     - ENTORNO ................:                             **
      **     - BASE DE DATOS ..........:                             **
      **                                                             **
      **  DESCRIPCION ......:                                        **
      **                                                             **
      **    - Recibe por LINKAGE un texto de 50 caracteres y una     **
      **      longitud de resultado deseado (Z)                      **
      **      Devuelve el texto util de entrada con X espacios       **
      **      delante e Y espacios detras, de forma a que el texto   **
      **      util de entrada este centrado en las Z posiciones de   **
      **      texto resultado.                                       **
      **                                                             **
      **  LOG DE MODIFICACIONES:                                     **
      **  FECHA----- DESCRIPCION------------------------------ BUSCA **
      **  2024-09-24 Multiusuario (MOSSA)                      USER  **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      *****************************************************************
       ENVIRONMENT DIVISION.
      *
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *
       DATA DIVISION.
      *----------------------------------------------------------------
       WORKING-STORAGE SECTION.
      *
       01  FILLER                          PIC X(19)
           VALUE '***START-WORKING***'.
      *
      *
       01  WX-VARIABLES.
           03  I                           PIC 999  VALUE 0.
           03  J                           PIC 999  VALUE 0.
           03  MAX-LENGTH                  PIC 999  VALUE 132.
           03  W-TEMP-STRING               PIC X(132).
      *
       01 FILLER                           PIC X(17)
           VALUE '***END-WORKING***'.
      *----------------------------------------------------------------
       LINKAGE SECTION.
      *
           COPY LKCENTI.
      *
      *================================================================
       PROCEDURE DIVISION USING RTC-CENTI.
      *----------------------------------------------------------------
      *--- Proceso principal                                        ---
      *----------------------------------------------------------------
       0000-PROCESO.
      *
           PERFORM INITIAL-CHECKS.
           PERFORM ADJUST-LEFT.
           PERFORM FIND-LAST-CHAR.
           PERFORM CENTER-STRING.
      *
           GOBACK.
      *----------------------------------------------------------------
      *--- Comprobaciones previas                                   ---
      *----------------------------------------------------------------
       INITIAL-CHECKS.
           INITIALIZE RTC-OUTPUT-DATA.
           IF RTC-INPUT-STRING EQUAL SPACES THEN
              SET RTC-NO-DATA-PROVIDED TO TRUE
           END-IF.
           IF RTC-OK THEN
              IF RTC-CENTER-AREA > MAX-LENGTH OR
                 RTC-CENTER-AREA = ZERO THEN
                 MOVE MAX-LENGTH TO RTC-CENTER-AREA
              END-IF
           END-IF.
      *----------------------------------------------------------------
      *--- Si es necesario, ajustamos a la izquierda                ---
      *----------------------------------------------------------------
       ADJUST-LEFT.
           IF RTC-OK THEN
              PERFORM VARYING I FROM 1 BY 1
                      UNTIL RTC-INPUT-STRING(I:1) NOT = SPACE
              END-PERFORM
              MOVE RTC-INPUT-STRING(I:) TO W-TEMP-STRING
           END-IF.
      *----------------------------------------------------------------
      *--- Nos situamos en el £ltimo caracter de la cadena          ---
      *----------------------------------------------------------------
       FIND-LAST-CHAR.
           IF RTC-OK THEN
              PERFORM VARYING I FROM RTC-CENTER-AREA BY -1
                      UNTIL W-TEMP-STRING(I:1) NOT = SPACE
              END-PERFORM
           END-IF.
      *----------------------------------------------------------------
      *--- Finalmente, centramos la cadena en el  rea indicada      ---
      *----------------------------------------------------------------
       CENTER-STRING.
           IF RTC-OK THEN
              IF I + 2 >= RTC-CENTER-AREA THEN
                 SET RTC-STRING-TO-BIG TO TRUE
              ELSE
      *--- Si hay margen, calculamos el centrado.
                 COMPUTE J = (RTC-CENTER-AREA - I) / 2
      *--- Movemos el texto a la posici¢n de centrado.
                 MOVE W-TEMP-STRING TO RTC-OUTPUT-STRING(J:)
              END-IF
           END-IF.
      *----------------------------------------------------------------
      *-------------------- FIN DE PROGRAMA  --------------------------
      *----------------------------------------------------------------
