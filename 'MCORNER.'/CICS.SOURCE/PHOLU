      *****************************************************************
      *                 IDENTIFICATION     DIVISION                   *
      *****************************************************************
       IDENTIFICATION DIVISION.
      *
       PROGRAM-ID.      PHOLU.
       AUTHOR.          LUIS MARTINEZ.
      *
      *****************************************************************
      **  PROYECTO .........: HOL                                    **
      **  PROGRAMA .........: PHOLU                                  **
      **  VERSION ..........: 1.0                                    **
      **  TITULO ...........: EMISIàN DE LISTADOS CON PARµMETROS     **
      **                                                             **
      **  TIPO .............:                                        **
      **     - LENGUAJE ...............: COBOL II                    **
      **     - ENTORNO ................: CICS                        **
      **     - BASE DE DATOS ..........: DB2                         **
      **                                                             **
      **  DESCRIPCION ......:                                        **
      **                                                             **
      **    - HOLU No recibe ninguna commarea especial desde HOLM    **
      **                                                             **
      **    - Permite generar listados por Batch con todos los       **
      **      par metros de Ordenaci¢n y Filtrado de la HOLS         **
      **                                                             **
      **    - Los listados se obtienen por escritura en Spool al     **
      **      Internal Reader de un JCL embebido en Working Storage  **
      **      Seg£n sean los par metros de pantalla, se validan, y   **
      **      se modifican los PARMS de ejecuci¢n din micamente,     **
      **      antes de cerrar el Spool, y por lo tanto, emitir el Job**
      **                                                             **
      **  LOG DE MODIFICACIONES:                                     **
      **  FECHA----- DESCRIPCION------------------------------ BUSCA **
      **  2024-01-19 Creaci¢n del programa                     CB001 **
      **  2024-08-18 Adaptaci¢n multiidioma (MOSSA)            TXMUL **
      **  2024-09-24 Multiusuario (MOSSA)                      USER  **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      *****************************************************************
       ENVIRONMENT DIVISION.
      *
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *
       DATA DIVISION.
      *----------------------------------------------------------------
       WORKING-STORAGE SECTION.
      *
      *--- Zona CICS --------------------------------------------------
      *
           COPY DFHAID.
           COPY DFHBMSCA.
           COPY CICSATTR.
           COPY HOLUMP.
           COPY DDCICS.
      *
      *--- Zona CICS fin ----------------------------------------------
      *
      *--- Zona DB2 ---------------------------------------------------
      *
           EXEC SQL
                INCLUDE SQLCA
           END-EXEC
      *
           EXEC SQL
                INCLUDE CLIENTES
           END-EXEC
      *
           EXEC SQL
                INCLUDE DDDB2
           END-EXEC
      *--- TXMUL ->
           EXEC SQL
                INCLUDE TABLAS
           END-EXEC
      *--- TXMUL <-
      *--- Zona DB2 fin -----------------------------------------------
      *
       01  FILLER                          PIC X(19)
           VALUE '***START-WORKING***'.
      *
       01  WC-CONSTANTES.
           03  WC-PROGRAMA                 PIC X(8)  VALUE 'PHOLU'.
           03  WC-TRANSACCION              PIC X(4)  VALUE 'HOLU'.
           03  WC-MAP                      PIC X(8)  VALUE 'HOLUMP'.
           03  WC-MAPSET                   PIC X(8)  VALUE 'HOLUMP'.
           03  WC-CURPOS                   PIC S9(4) VALUE +355.
      *--- USER ->
      *    03  WC-LONGITUD-TITULO          PIC 99    VALUE 69.
      *--- USER <-
           03  WC-JCL-LENGTH               PIC 9(8)  COMP VALUE 80.
      *
       01  SW-SWITCHES.
           03 SW-ENVIO-MAPA                PIC X    VALUE '0'.
              88 ENVIO-ERASE                        VALUE '1'.
              88 ENVIO-DATAONLY                     VALUE '2'.
      *
           03 SW-ERROR-VALIDACION          PIC 999  VALUE 0.
              88 ERROR-VALIDACION                   VALUE 1.
           03 SW-ERROR-CRITERIOS           PIC 999  VALUE 0.
              88 ERROR-CRITERIOS                    VALUE 1.
           03 SW-ERROR-TIPOS               PIC 999  VALUE 0.
              88 ERROR-TIPOS                        VALUE 1.
           03 SW-ERROR-FILTROS             PIC 999  VALUE 0.
              88 ERROR-FILTROS                      VALUE 1.
      *
       01  WL-LITERALES.
           03  WL-FIN-SESION               PIC X(80) VALUE
               'La transaccion HOLU ha finalizado...'.
      *
       01  WX-PARMS.
           03  PARM-CUR1                   PIC X.
           03  PARM-CUR2                   PIC X.
           03  PARM-COMI                   PIC X.
           03  PARM-CONT                   PIC X.
           03  PARM-TXTO                   PIC X(35).
      *
       01  WX-VARIABLES.
           03  COUNT-ORD                   PIC 9.
           03  COUNT-ASC                   PIC 9.
           03  COUNT-COM                   PIC 9.
      *
       01  WC-ABEND.
           03  WC-SPOOL-OPEN               PIC X(4)   VALUE 'U701'.
           03  WC-SPOOL-WRITE              PIC X(4)   VALUE 'U702'.
           03  WC-SPOOL-CLOSE              PIC X(4)   VALUE 'U703'.
      *
       01  WX-ABEND                        PIC X(4)   VALUE SPACES.
       01  JCL-STATEMENT                   PIC X(80)  VALUE SPACES.
      *
      * Zona especial para CICS -> SPOOL
      *
       01  RESP                            PIC 9(8)   BINARY.
       01  RESP2                           PIC 9(8)   BINARY.
       01  TOKEN                           PIC X(8).
      *
      *--- Tabla para el JCL
      *--- OJO: Se a¤ade USER y PASSWORD para evitar problemas
      *    de autorizaci¢n al ejecutar el JCL, ya que el usuario
      *    que le entra al Batch ya ser¡a START2
      *
       01  INIT-JCL.
           03  FILLER PIC X(80) VALUE
           "//RUNBHOLL JOB MCORNER,'RUN-BHOLL',USER=IBMUSER,         ".
           03  FILLER PIC X(80) VALUE
           "//       PASSWORD=SYS1,MSGCLASS=A,CLASS=A,MSGLEVEL=(1,1) ".
           03  FILLER PIC X(80) VALUE
           "//RUNPGM   EXEC PGM=IKJEFT01,DYNAMNBR=20,REGION=4096K    ".
           03  FILLER PIC X(80) VALUE
           "//STEPLIB  DD  DISP=SHR,DSN=DSN910.DB9G.SDSNEXIT         ".
           03  FILLER PIC X(80) VALUE
           "//         DD  DISP=SHR,DSN=DSN910.SDSNLOAD              ".
           03  FILLER PIC X(80) VALUE
           "//         DD  DISP=SHR,DSN=MCORNER.BATCH.LOAD           ".
           03  FILLER PIC X(80) VALUE
           "//         DD  DISP=SHR,DSN=MCORNER.CALL.LOAD            ".
           03  FILLER PIC X(80) VALUE
           "//LSTOUT   DD  SYSOUT=A                                  ".
           03  FILLER PIC X(80) VALUE
           "//SYSPRINT DD  SYSOUT=A                                  ".
           03  FILLER PIC X(80) VALUE
           "//SYSTSPRT DD  SYSOUT=A                                  ".
           03  FILLER PIC X(80) VALUE
           "//SYSOUT   DD  SYSOUT=A                                  ".
           03  FILLER PIC X(80) VALUE
           "//SYSTSIN  DD *                                          ".
           03  FILLER PIC X(80) VALUE
           "  DSN SYSTEM(DB9G)                                       ".
           03  FILLER PIC X(80) VALUE
           "      RUN PROGRAM (BHOLL)                   -            ".
           03  FILLER PIC X(80) VALUE
           "          PLAN(BTHPLAN)                     -            ".
           03  FILLER PIC X(80) VALUE
           "          LIBRARY('MCORNER.BATCH.LOAD')     -            ".
           03  FILLER PIC X(80) VALUE
           "          PARMS('DANN                                   ')".
           03  FILLER PIC X(80) VALUE
           "  END                                                    ".
           03  FILLER PIC X(80) VALUE
           "/*                                                       ".
           03  FILLER PIC X(80) VALUE
           "                                                         ".
           03  FILLER PIC X(80) VALUE
           "                                                         ".
           03  FILLER PIC X(80) VALUE
           "                                                         ".
           03  FILLER PIC X(80) VALUE
           "                                                         ".
           03  FILLER PIC X(80) VALUE
           "                                                         ".
           03  FILLER PIC X(80) VALUE
           "                                                         ".
           03  FILLER PIC X(80) VALUE
           "                                                         ".
           03  FILLER PIC X(80) VALUE
           "                                                         ".
           03  FILLER PIC X(80) VALUE
           "                                                         ".
           03  FILLER PIC X(80) VALUE
           "                                                         ".
           03  FILLER PIC X(80) VALUE
           "                                                         ".
           03  FILLER PIC X(80) VALUE
           "                                                         ".
           03  FILLER PIC X(80) VALUE
           "                                                         ".
           03  FILLER PIC X(80) VALUE
           "                                                         ".
           03  FILLER PIC X(80) VALUE
           "                                                         ".
           03  FILLER PIC X(80) VALUE
           "                                                         ".
           03  FILLER PIC X(80) VALUE
           "                                                         ".
           03  FILLER PIC X(80) VALUE
           "                                                         ".
           03  FILLER PIC X(80) VALUE
           "                                                         ".
           03  FILLER PIC X(80) VALUE
           "                                                         ".
           03  FILLER PIC X(80) VALUE LOW-VALUES.
      *
       01  WT-JCL-TB REDEFINES INIT-JCL.
           03  JCL-LINE    PIC X(80) OCCURS 40 INDEXED BY I.
      *--- TXMUL ->
       77 IND                             PIC 99 VALUE ZERO.
      *--- TXMUL <-
      *
      *--- Zona para CALLs --------------------------------------------
      *
           COPY LKCENTI.
      *
       01 FILLER                           PIC X(17)
           VALUE '***END-WORKING***'.
      *----------------------------------------------------------------
       LINKAGE SECTION.
      *
       01 DFHCOMMAREA                      PIC X(2048).
      *
      *================================================================
       PROCEDURE DIVISION.
      *----------------------------------------------------------------
      *--- Proceso principal                                        ---
      *----------------------------------------------------------------
       0000-PROCESO-TAREA.
      *
           EXEC SQL
                INCLUDE PDOPCIDB
           END-EXEC
      *
           MOVE WC-TRANSACCION              TO WA-TRANSACCION
           MOVE WC-CURPOS                   TO CH-CURPOS
      *
           IF EIBCALEN > 0
              MOVE DFHCOMMAREA              TO CH-COMMAREA-HOL
           END-IF
      *
      *--- Evaluacion de Commarea y Teclas para identificar
      *--- los procesos que deban de ejecutarse
      *
           EVALUATE TRUE
      *
      *--- No hay Commarea
      *--- Se inicializa la Commarea y se envia mapa limpio
      *
               WHEN EIBCALEN = ZERO
                    MOVE LOW-VALUE          TO HOLUMPO
                    SET ENVIO-ERASE         TO TRUE
                    MOVE 'X'                TO CH-X-ACCION
                    MOVE DFHRED             TO MSGC
      *--- TXMUL ->
                    MOVE 'HOLUARR'          TO CL-CLAVE1
                    PERFORM 20100-OBTENER-TEXTO
                    MOVE CL-VALOR           TO MSGO
      *             MOVE 'Para arrancar HOLU utiliza HOLM... pulsa F3'
      *               TO MSGO
      *--- TXMUL <-
                    PERFORM 8000-ENVIO-MAPA
      *
      *--- Hay Commarea
      *--- El programa ha podido ser arrancado por XCTL desde otro
      *--- programa. en este caso la transaccion actual ser  la del
      *--- programa que ha llamado a este jhasta que se haga un
      *--- RETURN TRANSID
      *
               WHEN EIBCALEN > ZERO AND EIBTRNID NOT = WC-TRANSACCION
                    MOVE LOW-VALUE          TO HOLUMPO
                    SET ENVIO-ERASE         TO TRUE
                    PERFORM 8000-ENVIO-MAPA
      *
      *--- Borrado de pantalla  - Mensaje y retorno a CICS
      *--- F3 tambien puede terminar esta transaccion
      *
               WHEN EIBAID = DFHCLEAR
                    MOVE LOW-VALUE          TO HOLUMPO
                    PERFORM 8100-ENVIA-MENSAJE
                    PERFORM 8900-RETORNO-CICS
      *
      *--- Teclas de Att no hacemos nada
      *
               WHEN EIBAID = DFHPA1 OR DFHPA2 OR DFHPA3
                    CONTINUE
      *
      *--- Teclas de funcion Legales
      *--- Teclas que requieren un procesado del mapa
      *
               WHEN EIBAID = DFHENTER
                 OR EIBAID = DFHPF1
                 OR EIBAID = DFHPF3
                    PERFORM 1000-PROCESO-MAPA
      *
      *--- Teclas ilegales
      *--- Teclas no programadas que se detectaran en proceso de mapa
      *
               WHEN OTHER
                    PERFORM 1000-PROCESO-MAPA
      *
           END-EVALUATE
      *
      *--- Terminamos el proceso con Return TransId
      *--- Volvemos a enganchar esta misma transaccion
      *
           PERFORM 8200-RETORNO-TRANS.
      *
       0000-PROCESO-TAREA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Procesa la pantalla segun opcion elegida en menu         ---
      *----------------------------------------------------------------
       1000-PROCESO-MAPA.
      *
      * Recuperamos el mapa desde el terminal
      *
           PERFORM 8400-RECIBE-MAPA
      *
           EVALUATE TRUE
      *
      *--- Enter: Si la accion de CA es 'X' volvemos al menu
      *
              WHEN EIBAID = DFHENTER AND CH-X-ACCION = 'X'
                  PERFORM 3000-PROCESO-VOLVER
      *
      *--- Enter: Validamos el mapa y si es correcto proceso Enter
      *
              WHEN EIBAID = DFHENTER
                  PERFORM 2000-ACCION-MAPA
      *
      *--- F3: Se llama al programa de MENU (Volver)
      *
              WHEN EIBAID = DFHPF3
                   PERFORM 3000-PROCESO-VOLVER
      *
      *--- F1: Se llama al programa de ayuda online
      *
              WHEN EIBAID = DFHPF1
                   PERFORM 5000-PROCESO-AYUDA
      *
      *-- Otras teclas: Se avisa con mensaje en pantalla y mapa limpio
      *
              WHEN OTHER
      *--- TXMUL ->
                   MOVE 'KEYERR'           TO CL-CLAVE1
                   PERFORM 20100-OBTENER-TEXTO
                   MOVE CL-VALOR           TO MSGO
      *            MOVE 'Tecla erronea...' TO MSGO
      *--- TXMUL <-
      *
           END-EVALUATE.
      *
           SET ENVIO-DATAONLY              TO TRUE
           PERFORM 8000-ENVIO-MAPA.
      *
       1000-PROCESO-MAPA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Validamos y procesamos el mapa segun la accion de CA     ---
      *----------------------------------------------------------------
       2000-ACCION-MAPA.
      *
           PERFORM 2100-VALIDA-CAMPOS
      *
           IF NOT ERROR-VALIDACION
              PERFORM 2500-EJECUTA-ACCION
           END-IF.
      *
       2000-ACCION-MAPA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Validamos campos obligatorios segun accion               ---
      *----------------------------------------------------------------
       2100-VALIDA-CAMPOS.
      *
           MOVE 0                          TO SW-ERROR-VALIDACION
           MOVE 0                          TO SW-ERROR-CRITERIOS
           MOVE 0                          TO SW-ERROR-TIPOS
           MOVE 0                          TO SW-ERROR-FILTROS
           MOVE 0                          TO COUNT-ORD
           MOVE 0                          TO COUNT-ASC
           MOVE 0                          TO COUNT-COM
      *
           IF ODNII NOT = 'S'          AND
              ODNII NOT = SPACES       AND
              ODNII NOT = HIGH-VALUES  AND
              ODNII NOT = LOW-VALUES
              MOVE LOW-VALUES              TO ODNIO
           END-IF
      *
           IF ONOMI NOT = 'S'          AND
              ONOMI NOT = SPACES       AND
              ONOMI NOT = HIGH-VALUES  AND
              ONOMI NOT = LOW-VALUES
              MOVE LOW-VALUES              TO ONOMO
           END-IF
      *
           IF OAPEI NOT = 'S'          AND
              OAPEI NOT = SPACES       AND
              OAPEI NOT = HIGH-VALUES  AND
              OAPEI NOT = LOW-VALUES
              MOVE LOW-VALUES              TO OAPEO
           END-IF
      *
           IF OASCI NOT = 'S'          AND
              OASCI NOT = SPACES       AND
              OASCI NOT = HIGH-VALUES  AND
              OASCI NOT = LOW-VALUES
              MOVE LOW-VALUES              TO OASCO
           END-IF
      *
           IF ODESI NOT = 'S'          AND
              ODESI NOT = SPACES       AND
              ODESI NOT = HIGH-VALUES  AND
              ODESI NOT = LOW-VALUES
              MOVE LOW-VALUES              TO ODESO
           END-IF
      *
           IF FCOMI NOT = 'S'          AND
              FCOMI NOT = SPACES       AND
              FCOMI NOT = HIGH-VALUES  AND
              FCOMI NOT = LOW-VALUES
              MOVE LOW-VALUES              TO FCOMO
           END-IF
      *
           IF FCONI NOT = 'S'          AND
              FCONI NOT = SPACES       AND
              FCONI NOT = HIGH-VALUES  AND
              FCONI NOT = LOW-VALUES
              MOVE LOW-VALUES              TO FCONO
           END-IF
      *
           PERFORM 2110-VALIDA-CRITERIOS
           IF NOT ERROR-CRITERIOS
              PERFORM 2120-VALIDA-TIPOS
              IF NOT ERROR-TIPOS
                 PERFORM 2130-VALIDA-FILTROS
              END-IF
           END-IF
      *
           IF ERROR-CRITERIOS   OR
              ERROR-TIPOS       OR
              ERROR-FILTROS
              SET ERROR-VALIDACION         TO TRUE
           END-IF.
      *
       2100-VALIDA-CAMPOS-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Validar criterios de ordenaci¢n                          ---
      *----------------------------------------------------------------
       2110-VALIDA-CRITERIOS.
      *
           IF ODNII = 'S'
              ADD 1                        TO COUNT-ORD
           END-IF
           IF ONOMI = 'S'
              ADD 1                        TO COUNT-ORD
           END-IF
           IF OAPEI = 'S'
              ADD 1                        TO COUNT-ORD
           END-IF
      *
           IF COUNT-ORD NOT = 1
              MOVE DFHRED                  TO MSGC
      *--- TXMUL ->
              MOVE 'SELCRIS'               TO CL-CLAVE1
              PERFORM 20100-OBTENER-TEXTO
              MOVE CL-VALOR                TO MSGO
      *       MOVE 'Seleccionar un criterio y uno solo, con "S"'
      *         TO MSGO
      *--- TXMUL <-
              move count-ord to msgo(78:1)
              SET ERROR-CRITERIOS          TO TRUE
           END-IF.
      *
       2110-VALIDA-CRITERIOS-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Validar tipos de ordenaci¢n                              ---
      *----------------------------------------------------------------
       2120-VALIDA-TIPOS.
      *
           IF OASCI = 'S'
              ADD 1                        TO COUNT-ASC
           END-IF
           IF ODESI = 'S'
              ADD 1                        TO COUNT-ASC
           END-IF
      *
           IF COUNT-ASC NOT = 1
              MOVE DFHRED                  TO MSGC
      *--- TXMUL ->
              MOVE 'SELORDS'               TO CL-CLAVE1
              PERFORM 20100-OBTENER-TEXTO
              MOVE CL-VALOR                TO MSGO
      *       MOVE 'Seleccionar solo un tipo de ¢rden con "S"'
      *         TO MSGO
      *--- TXMUL <-
              SET ERROR-TIPOS              TO TRUE
           END-IF.
      *
       2120-VALIDA-TIPOS-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Validar filtros de ordenaci¢n                            ---
      *----------------------------------------------------------------
       2130-VALIDA-FILTROS.
      *
           IF FCOMI = 'S'
              ADD 1                        TO COUNT-COM
           END-IF
           IF FCONI = 'S'
              ADD 1                        TO COUNT-COM
           END-IF
           IF COUNT-COM NOT = 1 AND COUNT-COM NOT = 0
              MOVE DFHRED                  TO MSGC
      *--- TXMUL ->
              MOVE 'FILERRS'               TO CL-CLAVE1
              PERFORM 20100-OBTENER-TEXTO
              MOVE CL-VALOR                TO MSGO
      *       MOVE 'Filtros incoherentes. Solo uno con "S" o ninguno'   "'
      *         TO MSGO
      *--- TXMUL <-
              SET ERROR-FILTROS            TO TRUE
           END-IF
           IF NOT ERROR-FILTROS AND
              (FCOMI = 'S' OR FCONI = 'S') AND
              (TEXTI = SPACES OR
               TEXTI = LOW-VALUES OR
               TEXTI = HIGH-VALUES)
               SET ERROR-FILTROS           TO TRUE
               MOVE DFHRED                 TO MSGC
      *--- TXMUL ->
               MOVE 'FILTXTB'              TO CL-CLAVE1
               PERFORM 20100-OBTENER-TEXTO
               MOVE CL-VALOR               TO MSGO
      *        MOVE 'Filtrar implica un texto a buscar'
      *          TO MSGO
      *--- TXMUL <-
           END-IF.
      *
       2130-VALIDA-FILTROS-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Ejecutamos la accion segun opcion de CA                  ---
      *----------------------------------------------------------------
       2500-EJECUTA-ACCION.
      *
           EVALUATE TRUE
              WHEN ODNII = 'S'
                   MOVE 'D'                TO PARM-CUR1
              WHEN ONOMI = 'S'
                   MOVE 'N'                TO PARM-CUR1
              WHEN OAPEI = 'S'
                   MOVE 'A'                TO PARM-CUR1
           END-EVALUATE
      *
           EVALUATE TRUE
              WHEN OASCI = 'S'
                   MOVE 'A'                TO PARM-CUR2
              WHEN ODESI = 'S'
                   MOVE 'D'                TO PARM-CUR2
           END-EVALUATE
      *
           IF FCOMI = 'S'
              MOVE 'S'                     TO PARM-COMI
           ELSE
              MOVE 'N'                     TO PARM-COMI
           END-IF
           IF FCONI = 'S'
              MOVE 'S'                     TO PARM-CONT
           ELSE
              MOVE 'N'                     TO PARM-CONT
           END-IF
           IF (TEXTI NOT = SPACES      AND
               TEXTI NOT = LOW-VALUES  AND
               TEXTI NOT = HIGH-VALUES)  AND
               (FCOMI = 'S' OR FCONI = 'S')
              MOVE TEXTI                   TO PARM-TXTO
           ELSE
              MOVE SPACES                  TO PARM-TXTO
              MOVE SPACES                  TO TEXTO
           END-IF.
      *
      *--- Modificamos los PARMS del JCL y lo lanzamos
      *
           MOVE JCL-LINE(17)               TO JCL-STATEMENT
           MOVE WX-PARMS                   TO JCL-STATEMENT(18:39)
           MOVE JCL-STATEMENT              TO JCL-LINE(17)
      *
           PERFORM 6100-ABRE-SPOOL
           SET I                           TO 1
           PERFORM 6200-ESCRIBE-SPOOL
              UNTIL I > 40                    OR
                    JCL-LINE(I) = LOW-VALUES  OR
                    JCL-LINE(I) = SPACES
           PERFORM 6300-CIERRA-SPOOL
           MOVE DFHYELLO                   TO MSGC
      *--- TXMUL ->
           MOVE 'LISSPOO'                  TO CL-CLAVE1.
           PERFORM 20100-OBTENER-TEXTO.
           MOVE CL-VALOR                   TO MSGO.
      *    MOVE 'Listado lanzado al Spool' TO MSGO.
      *--- TXMUL <-
      *
       2500-EJECUTA-ACCION-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Llamamos al programa de MENU (Volver)                    ---
      *----------------------------------------------------------------
       3000-PROCESO-VOLVER.
      *
           MOVE CS-PGM-CLIENTES            TO CH-XCTL
           MOVE WC-TRANSACCION             TO CH-TRANSACCION
           MOVE WC-TRANSACCION             TO CH-TRANS-RETORNO
           MOVE WC-PROGRAMA                TO CH-PROGRAMA-RETORNO
           PERFORM 8700-XCTL-PROGRAMA.
      *
       3000-PROCESO-VOLVER-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Procesa la tecla F1                                      ---
      *---    Llamamos al programa de visualizacion de ayuda        ---
      *----------------------------------------------------------------
       5000-PROCESO-AYUDA.
      *
           MOVE CS-PGM-AYUDAOL             TO CH-XCTL
           MOVE WC-TRANSACCION             TO CH-TRANSACCION
           MOVE WC-TRANSACCION             TO CH-TRANS-RETORNO
           MOVE WC-PROGRAMA                TO CH-PROGRAMA-RETORNO
           PERFORM 8700-XCTL-PROGRAMA.
      *
       5000-PROCESO-AYUDA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Abre el archivo de escritura en el Spool                 ---
      *----------------------------------------------------------------
       6100-ABRE-SPOOL.
      *
           EXEC CICS SPOOLOPEN OUTPUT
                TOKEN(TOKEN)
                USERID('INTRDR')
                NODE('N1')
                CLASS('A')
                NOHANDLE
                RESP(RESP)
                RESP2(RESP2)
           END-EXEC
      *
           IF EIBRESP NOT = 0
              MOVE WC-SPOOL-OPEN           TO WX-ABEND
              PERFORM 8600-CICS-ABEND
           END-IF.
      *
       6100-ABRE-SPOOL-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Escribe una l¡nea en el Spool                            ---
      *----------------------------------------------------------------
       6200-ESCRIBE-SPOOL.
      *
           MOVE JCL-LINE(I)                TO JCL-STATEMENT
      *
           EXEC CICS SPOOLWRITE
                FROM(JCL-STATEMENT)
                NOHANDLE
                FLENGTH(WC-JCL-LENGTH)
                TOKEN(TOKEN)
           END-EXEC
      *
           IF EIBRESP NOT = 0
              MOVE WC-SPOOL-WRITE          TO WX-ABEND
              PERFORM 8600-CICS-ABEND
           END-IF
      *
           SET I                           UP BY 1.
      *
       6200-ESCRIBE-SPOOL-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Cierra el archivo de Spool                               ---
      *----------------------------------------------------------------
       6300-CIERRA-SPOOL.
      *
           EXEC CICS SPOOLCLOSE
                TOKEN(TOKEN)
                NOHANDLE
           END-EXEC
      *
           IF EIBRESP NOT = 0
              MOVE WC-SPOOL-CLOSE          TO WX-ABEND
              PERFORM 8600-CICS-ABEND
           END-IF.
      *
       6300-CIERRA-SPOOL-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Envia el mapa al terminal sergun modo del switch         ---
      *----------------------------------------------------------------
       8000-ENVIO-MAPA.
      *
           EVALUATE TRUE
      *
      *--- Envio de mapa limpio
      *
               WHEN ENVIO-ERASE
      *
      *--- TXMUL ->
                   MOVE 50 TO RTC-CENTER-AREA
                   PERFORM 20000-TITULO-MAPA
      * Carga de los textos fijos del mapa
                   MOVE WC-TRANSACCION TO TB-CLAVE1
                   PERFORM 20200-OPEN-CURSOR-MAPS
                   PERFORM WITH TEST AFTER UNTIL SQLCODE = 100
                       PERFORM 20200-FETCH-CURSOR-MAPS
                       IF SQLCODE NOT = 100 AND
                          TB-CLAVE3 = CL-IDIOMA THEN
                          ADD 1 TO IND
                          EVALUATE IND
                             WHEN 1
                                MOVE TB-VALOR TO TX10O
                             WHEN 2
                                MOVE TB-VALOR TO TX20O
                             WHEN 3
                                MOVE TB-VALOR TO TX30O
                             WHEN 4
                                MOVE TB-VALOR TO TX40O
                             WHEN 5
                                MOVE TB-VALOR TO TX50O
                             WHEN 6
                                MOVE TB-VALOR TO TX60O
                             WHEN 7
                                MOVE TB-VALOR TO TX70O
                             WHEN 8
                                MOVE TB-VALOR TO TX80O
                             WHEN 9
                                MOVE TB-VALOR TO TX90O
                             WHEN 10
                                MOVE TB-VALOR TO TX100O
                             WHEN 11
                                MOVE TB-VALOR TO TX110O
                             WHEN 12
                                MOVE TB-VALOR TO TX120O
                             WHEN 13
                                MOVE TB-VALOR TO TX130O
                          END-EVALUATE
                       END-IF
                   END-PERFORM
                   PERFORM 20200-CLOSE-CURSOR-MAPS
      *--- TXMUL <-
      *
                   EXEC CICS
                       SEND MAP    ('HOLUMP')
                            MAPSET ('HOLUMP')
                            FROM   (HOLUMPO)
                            CURSOR (CH-CURPOS)
                            ERASE
                   END-EXEC
      *
      *--- Envio del mapa Solo datos
      *
               WHEN ENVIO-DATAONLY
      *
                   EXEC CICS
                       SEND MAP     ('HOLUMP')
                            MAPSET  ('HOLUMP')
                            FROM    (HOLUMPO)
                            CURSOR  (CH-CURPOS)
                            DATAONLY
                   END-EXEC
      *
           END-EVALUATE.
      *
       8000-ENVIO-MAPA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Envia un mensaje al terminal del usuario (fin sesion)    ---
      *----------------------------------------------------------------
       8100-ENVIA-MENSAJE.
      *
      *--- TXMUL ->
           MOVE 'HOLUEND'                  TO CL-CLAVE1.
           PERFORM 20100-OBTENER-TEXTO.
           MOVE CL-VALOR                   TO WL-FIN-SESION.
      * 'La transaccion HOLU ha finalizado...'
      *--- TXMUL <-
           EXEC CICS
               SEND TEXT FROM(WL-FIN-SESION)
                         ERASE
                         FREEKB
           END-EXEC.
      *
       8100-ENVIA-MENSAJE-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Devuelve el control a una transaccion                    ---
      *----------------------------------------------------------------
       8200-RETORNO-TRANS.
      *
           EXEC CICS
               RETURN
               TRANSID      (WA-TRANSACCION)
               COMMAREA     (CH-COMMAREA-HOL)
           END-EXEC.
      *
       8200-RETORNO-TRANS-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Recibe el mapa de la tarea                               ---
      *----------------------------------------------------------------
       8400-RECIBE-MAPA.
      *
           EXEC CICS
                RECEIVE
                MAP     ('HOLUMP')
                MAPSET  ('HOLUMP')
                INTO    (HOLUMPI)
           END-EXEC
      *
           PERFORM 8410-MAYUSCULAS.
      *
       8400-RECIBE-MAPA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Pasa datos sensibles a mayusculas                        ---
      *----------------------------------------------------------------
       8410-MAYUSCULAS.
      *
           MOVE FUNCTION UPPER-CASE(ODNII)   TO ODNIO.
           MOVE FUNCTION UPPER-CASE(ONOMI)   TO ONOMO.
           MOVE FUNCTION UPPER-CASE(OAPEI)   TO OAPEO.
           MOVE FUNCTION UPPER-CASE(OASCI)   TO OASCO.
           MOVE FUNCTION UPPER-CASE(ODESI)   TO ODESO.
           MOVE FUNCTION UPPER-CASE(FCOMI)   TO FCOMO
           MOVE FUNCTION UPPER-CASE(FCONI)   TO FCONO.
      *
       8410-MAYUSCULAS-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Llama a otro programa CICS por LINK con Commarea          ---
      *----------------------------------------------------------------
       8500-LINK-PROGRAMA.
      *
           EXEC CICS
                LINK
                PROGRAM (CH-LINK)
                COMMAREA(CH-COMMAREA-HOL)
                LENGTH   (LENGTH OF CH-COMMAREA-HOL)
           END-EXEC.
      *
       8500-LINK-PROGRAMA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Terminaci¢n an¢mala de la tarea                          ---
      *----------------------------------------------------------------
       8600-CICS-ABEND.
      *
           EXEC CICS
                ABEND
                ABCODE(WX-ABEND)
                NODUMP
           END-EXEC.
      *
       8600-CICS-ABEND-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Cede el control a otro programa CICS                     ---
      *----------------------------------------------------------------
       8700-XCTL-PROGRAMA.
      *
           EXEC CICS
                SET TERMINAL(EIBTRMID) UCTRAN
           END-EXEC
      *
           EXEC CICS
               XCTL PROGRAM  (CH-XCTL)
                    COMMAREA (CH-COMMAREA-HOL)
                    RESP     (WA-RESPUESTA-CICS)
           END-EXEC.
      *
       8700-XCTL-PROGRAMA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- CEDF visualiza una traza - Quitar para produccion        ---
      *----------------------------------------------------------------
       8800-TRAZA.
      *
           EXEC CICS
               ENTER       TRACEID(0)
               FROM        (WA-TRAZA)
           END-EXEC.
      *
       8800-TRAZA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Devuelve el control a CICS - Salida total                ---
      *----------------------------------------------------------------
       8900-RETORNO-CICS.
      *
           EXEC CICS
                SET TERMINAL(EIBTRMID) UCTRAN
           END-EXEC
      *
           EXEC CICS
                RETURN
           END-EXEC.
      *
       8900-RETORNO-CICS-EXIT.
           EXIT.
      *--- TXMUL ->
      *
      * Rutinas multiidioma
      *
           EXEC SQL
                INCLUDE PTXMUL
           END-EXEC.
      *
       999999-DB2-ERRORES SECTION.
      *--- TXMUL <-
      *----------------------------------------------------------------
      *--- DB2 Errores y otros                                      ---
      *----------------------------------------------------------------
           EXEC SQL
                INCLUDE PDDB2
           END-EXEC.
      *----------------------------------------------------------------
      *-------------------- FIN DE PROGRAMA  --------------------------
      *----------------------------------------------------------------
