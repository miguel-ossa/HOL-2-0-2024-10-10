      *****************************************************************
      *                 IDENTIFICATION     DIVISION                   *
      *****************************************************************
       IDENTIFICATION DIVISION.
      *
       PROGRAM-ID.      PTST1.
       AUTHOR.          LUIS MARTINEZ.
      *
      *****************************************************************
      **  PROYECTO .........: HOL                                    **
      **  PROGRAMA .........: PTST1                                  **
      **  VERSION ..........: 1.0                                    **
      **  TITULO ...........: PROGRAMA PARA PRUEBAS CICS             **
      **                                                             **
      **  TIPO .............:                                        **
      **     - LENGUAJE ...............: COBOL II                    **
      **     - ENTORNO ................: CICS                        **
      **     - BASE DE DATOS ..........: DB2                         **
      **                                                             **
      **  DESCRIPCION ......:                                        **
      **                                                             **
      **    - Acepta un DNI por pantalla y accede a DB2 para ver     **
      **      si existe algun registro en la tabla CLIENETS con      **
      **      esa clave primaria. Indica si el registro existe o no  **
      **      con un mensaje en pantalla o mostrando el nombre       **
      **                                                             **
      **  LOG DE MODIFICACIONES:                                     **
      **  FECHA----- DESCRIPCION------------------------------ BUSCA **
      **  2024-08-18 Adaptaci¢n multiidioma (MOSSA)            TXMUL **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      *****************************************************************
       ENVIRONMENT DIVISION.
      *
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *
       DATA DIVISION.
      *----------------------------------------------------------------
       WORKING-STORAGE SECTION.
      *
      *--- Zona CICS --------------------------------------------------
      *
       COPY DFHAID.
       COPY DFHBMSCA.
       COPY TST1MP.
      * --- CICS COMMAREA APLICACION HOL -----------------------------
       COPY DDCICS.
      *
      *--- Zona CICS fin ----------------------------------------------
      *
      *--- Zona DB2 ---------------------------------------------------
      *
           EXEC SQL
                INCLUDE SQLCA
           END-EXEC
      *
           EXEC SQL
                INCLUDE CLIENTES
           END-EXEC.
      * --- AREA DE DB2 PARA TRATAMIENTO DE ERRORES (VARIABLES HOST) -
           EXEC SQL
                INCLUDE DDDB2
           END-EXEC
      *--- TXMUL ->
           EXEC SQL
                INCLUDE TABLAS
           END-EXEC
      *--- TXMUL <-
      *--- Zona DB2 fin -----------------------------------------------
      *
       01  FILLER                          PIC X(19)
           VALUE '***START-WORKING***'.
      *
       01  WC-CONSTANTES.
           03  WC-PROGRAMA                 PIC X(8)  VALUE 'PTST1'.
           03  WC-TRANSACCION              PIC X(4)  VALUE 'TST1'.
           03  WC-MAP                      PIC X(8)  VALUE 'TST1MP'.
           03  WC-MAPSET                   PIC X(8)  VALUE 'TST1MP'.
           03  WC-CURPOS                   PIC S9(4) VALUE +580.
           03  WC-COMMAREA-LENGTH          PIC S9(4) COMP VALUE 1024.
           03  WC-LONGITUD-TITULO          PIC 99    VALUE 74.
      *
       01  SW-SWITCHES.
           03 SW-ENVIO-MAPA                PIC X    VALUE '0'.
              88 ENVIO-ERASE                        VALUE '1'.
              88 ENVIO-DATAONLY                     VALUE '2'.
           03 SW-ERROR-VALIDACION          PIC 999  VALUE 0.
              88 DNI-VACIO                          VALUE 1.
      *
       01  WL-LITERALES.
           03  WL-FIN-SESION               PIC X(80) VALUE
               'La transaccion TST1 ha finalizado...'.
      *--- TXMUL ->
       77 IND                             PIC 99 VALUE ZERO.
      *--- TXMUL <-
      *--- USER ->
       77 USERO                           PIC X(6).
      *--- USER <-
      *
      *--- Zona para CALLs --------------------------------------------
      *
           COPY LKCENTI.
      *
       01 FILLER                           PIC X(17)
           VALUE '***END-WORKING***'.
      *----------------------------------------------------------------
       LINKAGE SECTION.
      *
       01 DFHCOMMAREA                      PIC X(2048).
      *
      *================================================================
       PROCEDURE DIVISION.
      *----------------------------------------------------------------
      *--- Proceso principal                                        ---
      *----------------------------------------------------------------
       0000-PROCESO-TAREA.
      *
      *--- CONTROL DE OPCIONES PARA PROGRAMAS CICS CON DB2          ---
      *
           EXEC SQL
                INCLUDE PDOPCIDB
           END-EXEC
      *
           MOVE WC-TRANSACCION              TO WA-TRANSACCION
      *
           IF EIBCALEN > 0
              MOVE DFHCOMMAREA              TO CH-COMMAREA-HOL
           END-IF
      *
      *--- Evaluacion de Commarea y Teclas para identificar
      *--- los procesos que deban de ejecutarse
      *
           EVALUATE TRUE
      *
      *--- No hay Commarea
      *--- Se inicializa la Commarea y se envia mapa limpio
      *
               WHEN EIBCALEN = ZERO
                    MOVE LOW-VALUE          TO TST1MPO
                    INITIALIZE CH-COMMAREA-HOL
                    SET ENVIO-ERASE         TO TRUE
                    PERFORM 8000-ENVIO-MAPA
      *
      *--- Hay Commarea
      *--- El programa ha podido ser arrancado por XCTL desde otro
      *--- programa con XCTL como retorno al actual. En este caso
      *--- el campo ch-programa-retorno contiene algun valor.
      *--- En este caso inicializamos la Commarea y eviamos mapa limpio
      *
               WHEN EIBCALEN > ZERO AND CH-TRANS-RETORNO NOT = SPACES
                    MOVE LOW-VALUE          TO TST1MPO
                    INITIALIZE CH-COMMAREA-HOL
                    SET ENVIO-ERASE         TO TRUE
                    PERFORM 8000-ENVIO-MAPA
      *
      *--- Borrado de pantalla  - Mensaje y retorno a CICS
      *--- F3 tambien puede terminar esta transaccion
      *
               WHEN EIBAID = DFHCLEAR OR EIBAID = DFHPF3
                    MOVE LOW-VALUE          TO TST1MPO
                    PERFORM 8100-ENVIA-MENSAJE
                    PERFORM 8900-RETORNO-CICS
      *
      *--- Teclas de Att no hacemos nada
      *
               WHEN EIBAID = DFHPA1 OR DFHPA2 OR DFHPA3
                    CONTINUE
      *
      *--- Teclas de funcion Legales
      *--- Teclas que requieren un procesado del mapa
      *
               WHEN EIBAID = DFHENTER
                 OR EIBAID = DFHPF2
                 OR EIBAID = DFHPF1
                 OR EIBAID = DFHPF4
                    PERFORM 1000-PROCESO-MAPA
      *
      *--- Teclas ilegales
      *--- Teclas no programadas que se detectaran en proceso de mapa
      *
               WHEN OTHER
                    PERFORM 1000-PROCESO-MAPA
      *
           END-EVALUATE
      *
      *--- Terminamos el proceso con Return TransId
      *--- Volvemos a enganchar esta misma transaccion
      *
           PERFORM 8200-RETORNO-TRANS.
      *
       0000-PROCESO-TAREA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Procesa la pantalla segun tecla elegida por el usuario   ---
      *----------------------------------------------------------------
       1000-PROCESO-MAPA.
      *
      * Recuperamos el mapa desde el terminal
      *
           PERFORM 8400-RECIBE-MAPA
      *
           EVALUATE TRUE
      *
      *--- Enter: Validamos el mapa y si es correcto proceso Enter
      *
              WHEN EIBAID = DFHENTER
                   PERFORM 1100-VALIDA-MAPA
                   IF NOT DNI-VACIO
                      PERFORM 1200-PROCESO-ENTER
                      SET ENVIO-DATAONLY   TO TRUE
                   ELSE
                      SET ENVIO-DATAONLY   TO TRUE
                   END-IF
      *
      *--- F1: Se llama al programa de ayuda online
      *
              WHEN EIBAID = DFHPF1
                   PERFORM 1400-PROCESO-PF1
      *
      *--- F2: Se fuerza un error simulado de DB2 (Pruebas)
      *
              WHEN EIBAID = DFHPF2
                   PERFORM 1300-PROCESO-PF2
      *
      *--- F4: Obtener la longitud de un area de memoria
      *
              WHEN EIBAID = DFHPF4
                   PERFORM 1500-PROCESO-PF4
      *
      *-- Otras teclas: Se avisa con mensaje en pantalla y mapa limpio
      *
              WHEN OTHER
      *--- TXMUL ->
                   MOVE 'KEYERR'           TO CL-CLAVE1
                   PERFORM 20100-OBTENER-TEXTO
                   MOVE CL-VALOR           TO MSGO
      *            MOVE 'Tecla erronea...' TO MSGO
      *--- TXMUL <-
                   SET ENVIO-ERASE         TO TRUE
      *
           END-EVALUATE.
      *
           SET ENVIO-DATAONLY              TO TRUE
           PERFORM 8000-ENVIO-MAPA.
      *
       1000-PROCESO-MAPA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Valida los datos del mapa                                ---
      *----------------------------------------------------------------
       1100-VALIDA-MAPA.
      *
           IF FDNII = SPACES        OR
              FDNII = HIGH-VALUES   OR
              FDNII = LOW-VALUES
      *
      *--- DNI es campo obligatorio
      *
              SET DNI-VACIO                TO TRUE
      *--- TXMUL ->
              MOVE 'DNIOBL'                TO CL-CLAVE1
              PERFORM 20100-OBTENER-TEXTO
              MOVE CL-VALOR                TO MSGO
      *       MOVE 'El campo DNI debe rellenarse...'
      *         TO MSGO
      *--- TXMUL <-
           END-IF.
      *
       1100-VALIDA-MAPA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Procesa la tecla ENTER - Acceso a tabla OK/KO            ---
      *----------------------------------------------------------------
       1200-PROCESO-ENTER.
      *
           MOVE '1200-PROCESO-ENTER'       TO WA-PARRAFO
      *
           MOVE FDNII                      TO CL-DNI
           PERFORM 7000-DB2-CLIENTES-PK
      *
           IF SQLCODE = 0
              MOVE CL-NOMBRE               TO MSGO
           ELSE
      *--- TXMUL ->
              MOVE 'KEYERR'                TO CL-CLAVE1
              PERFORM 20100-OBTENER-TEXTO
              MOVE CL-VALOR                TO MSGO
      *       MOVE 'No existe este DNI...' TO MSGO
      *--- TXMUL <-
           END-IF.
      *
       1200-PROCESO-ENTER-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Procesa la tecla F2                                      ---
      *---    Simulamos una deteccion de error de DB2 que provoca   ---
      *---    una interrupcion automatica de flujo de programa    ---
      *---    que a todos los efectos es como un GO TO de COBOL   ---
      *----------------------------------------------------------------
       1300-PROCESO-PF2.
      *
           GO TO G999-ERROR-DB2.
      *
       1300-PROCESO-PF2-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Procesa la tecla F1                                      ---
      *---    Llamamos al programa de visualizacion de ayuda        ---
      *----------------------------------------------------------------
       1400-PROCESO-PF1.
      *
           MOVE CS-PGM-AYUDAOL             TO CH-XCTL
           MOVE WC-TRANSACCION             TO CH-TRANSACCION
           MOVE WC-TRANSACCION             TO CH-TRANS-RETORNO
           MOVE WC-PROGRAMA                TO CH-PROGRAMA-RETORNO
           PERFORM 8700-LLAMAR-PROGRAMA.
      *
       1400-PROCESO-PF1-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Procesa la tecla F4                                      ---
      *---    Muestra la longitud de la COMMAREA                    ---
      *----------------------------------------------------------------
       1500-PROCESO-PF4.
      *
           MOVE LENGTH OF CH-COMMAREA-HOL      TO FLONO.
      *
       1500-PROCESO-PF4-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- DB2 - SELECT CLIENTES POR DNI (PK)                       ---
      *----------------------------------------------------------------
       7000-DB2-CLIENTES-PK.
      *
           EXEC SQL
                SELECT
                   DNI
                 , NOMBRE
                 , APELLIDOS
                 , DIRECCION
                INTO
                   :CL-DNI
                 , :CL-NOMBRE
                 , :CL-APELLIDOS
                 , :CL-DIRECCION
                FROM CLIENTES
                WHERE
                   DNI = :CL-DNI
           END-EXEC.
      *
       7000-DB2-CLIENTES-PK-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Envia el mapa al terminal sergun modo del switch         ---
      *----------------------------------------------------------------
       8000-ENVIO-MAPA.
      *
           EVALUATE TRUE
               WHEN ENVIO-ERASE
      *
      *--- Envio de mapa limpio
      *
      *--- TXMUL ->
                   MOVE 70 TO RTC-CENTER-AREA
                   PERFORM 20000-TITULO-MAPA
      * Carga de los textos fijos del mapa
                   MOVE WC-TRANSACCION TO TB-CLAVE1
                   PERFORM 20200-OPEN-CURSOR-MAPS
                   PERFORM WITH TEST AFTER UNTIL SQLCODE = 100
                       PERFORM 20200-FETCH-CURSOR-MAPS
                       IF SQLCODE NOT = 100 AND
                          TB-CLAVE3 = CL-IDIOMA THEN
                          ADD 1 TO IND
                          EVALUATE IND
                             WHEN 1
                                MOVE TB-VALOR TO TX10O
                             WHEN 2
                                MOVE TB-VALOR TO TX20O
                             WHEN 3
                                MOVE TB-VALOR TO TX30O
                             WHEN 4
                                MOVE TB-VALOR TO TX40O
                             WHEN 5
                                MOVE TB-VALOR TO TX50O
                             WHEN 6
                                MOVE TB-VALOR TO TX60O
                          END-EVALUATE
                       END-IF
                   END-PERFORM
                   PERFORM 20200-CLOSE-CURSOR-MAPS
      *--- TXMUL <-
                   EXEC CICS
                       SEND MAP    ('TST1MP')
                            MAPSET ('TST1MP')
                            FROM   (TST1MPO)
                            ERASE
                   END-EXEC
      *
      *--- Envio del mapa Solo datos
      *
               WHEN ENVIO-DATAONLY
                   EXEC CICS
                       SEND MAP     ('TST1MP')
                            MAPSET  ('TST1MP')
                            FROM    (TST1MPO)
                            DATAONLY
                   END-EXEC
      *
           END-EVALUATE.
      *
       8000-ENVIO-MAPA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Envia un mensaje al terminal del usuario (fin sesion)    ---
      *----------------------------------------------------------------
       8100-ENVIA-MENSAJE.
      *
      *--- TXMUL ->
           MOVE 'HOL1END'                  TO CL-CLAVE1.
           PERFORM 20100-OBTENER-TEXTO.
           MOVE CL-VALOR                   TO WL-FIN-SESION.
      * 'La transaccion TST1 ha finalizado...'
      *--- TXMUL <-
           EXEC CICS
               SEND TEXT FROM(WL-FIN-SESION)
                         ERASE
                         FREEKB
           END-EXEC.
      *
       8100-ENVIA-MENSAJE-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Devuelve el control a una transaccion                    ---
      *----------------------------------------------------------------
       8200-RETORNO-TRANS.
      *
           EXEC CICS
               RETURN
               TRANSID      (WA-TRANSACCION)
               COMMAREA     (CH-COMMAREA-HOL)
           END-EXEC.
      *
       8200-RETORNO-TRANS-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Recibe el mapa de la tarea                               ---
      *----------------------------------------------------------------
       8400-RECIBE-MAPA.
      *
           EXEC CICS
                RECEIVE
                MAP     ('TST1MP')
                MAPSET  ('TST1MP')
                INTO    (TST1MPI)
           END-EXEC
      *
           PERFORM 8410-MAYUSCULAS.
      *
       8400-RECIBE-MAPA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Transforma a mayusculas los datos sensibles              ---
      *----------------------------------------------------------------
       8410-MAYUSCULAS.
      *
           MOVE FUNCTION UPPER-CASE(FDNII)  TO FDNIO.
      *
       8410-MAYUSCULAS-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Llama a otro programa CICS por LINK con Commarea         ---
      *----------------------------------------------------------------
       8500-LINK-PROGRAMA.
      *
           EXEC CICS
                LINK
                PROGRAM (CH-LINK)
                COMMAREA(CH-COMMAREA-HOL)
                LENGTH  (LENGTH OF CH-COMMAREA-HOL)
           END-EXEC.
      *
       8500-LINK-PROGRAMA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Cede el control a otro programa CICS                     ---
      *----------------------------------------------------------------
       8700-LLAMAR-PROGRAMA.
      *
           EXEC CICS
                SET TERMINAL(EIBTRMID) UCTRAN
           END-EXEC
      *
           EXEC CICS
               XCTL PROGRAM  (CH-XCTL)
                    COMMAREA (CH-COMMAREA-HOL)
                    RESP     (WA-RESPUESTA-CICS)
           END-EXEC.
      *
       8700-LLAMAR-PROGRAMA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- CEDF vicualiza una traza - Quitar para produccion        ---
      *----------------------------------------------------------------
       8800-TRAZA.
      *
           EXEC CICS
               ENTER       TRACEID(0)
               FROM        (WA-TRAZA)
           END-EXEC.
      *
       8800-TRAZA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Devuelve el control a CICS - Salida total                ---
      *----------------------------------------------------------------
       8900-RETORNO-CICS.
      *
           EXEC CICS
                SET TERMINAL(EIBTRMID) UCTRAN
           END-EXEC
      *
           EXEC CICS
                RETURN
           END-EXEC.
      *
       8900-RETORNO-CICS-EXIT.
           EXIT.
      *--- TXMUL ->
      *
      * Rutinas multiidioma
      *
           EXEC SQL
                INCLUDE PTXMUL
           END-EXEC.
      *
      *--- TXMUL <-
      *----------------------------------------------------------------
      *--- DB2 Un error ha sido detectado. Se ha forzado el proceso ---
      *----------------------------------------------------------------
       G999-ERROR-DB2.
      *
           IF EIBAID = DFHPF2
              MOVE WA-PARRAFO              TO CH-E-PARRAFO
              MOVE -111111111              TO DB2-CODE
              MOVE DB2-CODE                TO DB2-CODE-EDIT
              MOVE DB2-CODE-EDIT           TO CH-E-CODIGO
              MOVE 'ALGUN ERROR DB2'       TO CH-E-MENSAJE
              MOVE WC-PROGRAMA             TO CH-E-PROGRAMA
              MOVE WC-TRANSACCION          TO CH-E-TRANSACCION
              MOVE 'FORZAR ERROR'          TO CH-E-ACCION
              MOVE CS-PGM-ERROR            TO CH-XCTL
              MOVE WC-TRANSACCION          TO CH-TRANS-RETORNO
              MOVE WC-PROGRAMA             TO CH-PROGRAMA-RETORNO
              PERFORM 8700-LLAMAR-PROGRAMA
           ELSE
              MOVE WA-PARRAFO              TO CH-E-PARRAFO
              MOVE SQLCODE                 TO DB2-CODE
              MOVE DB2-CODE                TO DB2-CODE-EDIT
              MOVE DB2-CODE-EDIT           TO CH-E-CODIGO
              MOVE SQLERRMC                TO CH-E-MENSAJE
              MOVE WC-PROGRAMA             TO CH-E-PROGRAMA
              MOVE WC-TRANSACCION          TO CH-E-TRANSACCION
              MOVE 'Select PK Clientes'    TO CH-E-ACCION
              MOVE CS-PGM-ERROR            TO CH-XCTL
              MOVE WC-TRANSACCION          TO CH-TRANS-RETORNO
              MOVE WC-PROGRAMA             TO CH-PROGRAMA-RETORNO
              PERFORM 8700-LLAMAR-PROGRAMA
           END-IF.
      *
       G999-ERROR-DB2-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *-------------------- FIN DE PROGRAMA  --------------------------
      *----------------------------------------------------------------
