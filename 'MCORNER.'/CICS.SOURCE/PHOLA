      *****************************************************************
      *                 IDENTIFICATION     DIVISION                   *
      *****************************************************************
       IDENTIFICATION DIVISION.
      *
       PROGRAM-ID.      PHOLA.
       AUTHOR.          MIGUEL DE LA OSSA.
      *
      *****************************************************************
      **  PROYECTO .........: HOL                                    **
      **  PROGRAMA .........: PHOLA                                  **
      **  VERSION ..........: 1.0                                    **
      **  TITULO ...........: PANTALLA DE LOGIN                      **
      **                                                             **
      **  TIPO .............:                                        **
      **     - LENGUAJE ...............: COBOL II                    **
      **     - ENTORNO ................: CICS                        **
      **     - BASE DE DATOS ..........: DB2                         **
      **                                                             **
      **  DESCRIPCION ......:                                        **
      **                                                             **
      **    - Permite ingresar con un usuario en la aplicaci¢n.      **
      **                                                             **
      **                                                             **
      **  LOG DE MODIFICACIONES:                                     **
      **  FECHA----- DESCRIPCION------------------------------ BUSCA **
      **  AAAA-MM-DD XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX YYYYY **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      *****************************************************************
       ENVIRONMENT DIVISION.
      *
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *
       DATA DIVISION.
      *----------------------------------------------------------------
       WORKING-STORAGE SECTION.
      *
      *--- Zona CICS --------------------------------------------------
      *
           COPY DFHAID.
           COPY DFHBMSCA.
           COPY CICSATTR.
           COPY HOLAMP.
           COPY DDCICS.
      *
      *--- Zona CICS fin ----------------------------------------------
      *
      *--- Zona DB2 ---------------------------------------------------
      *
           EXEC SQL
                INCLUDE SQLCA
           END-EXEC
      *
           EXEC SQL
                INCLUDE DDDB2
           END-EXEC
      *
           EXEC SQL
                INCLUDE TABLAS
           END-EXEC
      *
           EXEC SQL
                INCLUDE USUARIOS
           END-EXEC
      *--- Zona DB2 fin -----------------------------------------------
       01  FILLER                          PIC X(19)
           VALUE '***START-WORKING***'.
       01  WC-CONSTANTES.
           03  WC-PROGRAMA                 PIC X(8)  VALUE 'PHOLA'.
           03  WC-TRANSACCION              PIC X(4)  VALUE 'HOLA'.
           03  WC-MAP                      PIC X(8)  VALUE 'HOLAMP'.
           03  WC-MAPSET                   PIC X(8)  VALUE 'HOLAMP'.
      *
           03  WC-CUR-USU                  PIC S9(4) COMP VALUE +600.
           03  WC-CUR-PSW                  PIC S9(4) COMP VALUE +680.
      *
       01  SW-SWITCHES.
           03 SW-ENVIO-MAPA                PIC X    VALUE '0'.
              88 ENVIO-ERASE                        VALUE '1'.
              88 ENVIO-DATAONLY                     VALUE '2'.
      *
           03 SW-ERROR-VALIDACION          PIC 999  VALUE 0.
              88 ERROR-VALIDACION                   VALUE 1.
      *
       01 CURRENT-TIME.
          03 TSTAMP     PIC S9(14) COMP.
          03 OUTDATE    PIC X(10) VALUE SPACES.
          03 OUTDATER REDEFINES OUTDATE.
             05 MM      PIC 99.
             05 S1      PIC X.
             05 DD      PIC 99.
             05 S2      PIC X.
             05 YYYY    PIC 9(4).
          03 OUTTIME    PIC X(8)  VALUE SPACES.
      *
       01 OUTDATEA.
          03 DD         PIC 99.
          03 S1         PIC X.
          03 MM         PIC 99.
          03 S2         PIC X.
          03 YYYY       PIC 9(4).
       01  WL-LITERALES.
           03  WL-FIN-SESION               PIC X(80) VALUE
               'La transaccion HOLA ha finalizado...'.
      *
       77 USERO                           PIC X(6).
       77 IND                             PIC 99 VALUE ZERO.
      *
      *--- Zona para CALLs --------------------------------------------
      *
           COPY LKCENTI.
      *
       01 FILLER                           PIC X(17)
           VALUE '***END-WORKING***'.
      *----------------------------------------------------------------
       LINKAGE SECTION.
      *
       01 DFHCOMMAREA                      PIC X(2048).
      *
      *================================================================
       PROCEDURE DIVISION.
      *----------------------------------------------------------------
      *--- Proceso principal                                        ---
      *----------------------------------------------------------------
       0000-PROCESO-TAREA.
      *
           EXEC SQL
                INCLUDE PDOPCIDB
           END-EXEC
      *
           MOVE WC-TRANSACCION              TO WA-TRANSACCION
      *
           IF EIBCALEN > 0
              MOVE DFHCOMMAREA              TO CH-COMMAREA-HOL
           END-IF
      *
      *--- Evaluacion de Commarea y Teclas para identificar
      *--- los procesos que deban de ejecutarse
      *
           EVALUATE TRUE
      *
      *--- No hay Commarea
      *--- Se inicializa la Commarea y se envia mapa limpio
               WHEN EIBCALEN = ZERO OR
                    EIBCALEN > ZERO AND EIBTRNID NOT = WC-TRANSACCION
                    PERFORM 2700-READ-LANGUAGE
                    MOVE LOW-VALUE          TO HOLAMPO
                    PERFORM 2710-ASKTIME
                    MOVE WC-CUR-USU         TO CH-CURPOS
                    SET ENVIO-ERASE         TO TRUE
                    PERFORM 8000-ENVIO-MAPA
      *--- Borrado de pantalla  - Mensaje y retorno a CICS
      *--- F3 tambien puede terminar esta transaccion
      *
               WHEN EIBAID = DFHCLEAR
                    MOVE LOW-VALUE          TO HOLAMPO
                    PERFORM 8100-ENVIA-MENSAJE
                    PERFORM 8900-RETORNO-CICS
      *
      *--- Teclas de Att no hacemos nada
      *
               WHEN EIBAID = DFHPA1 OR DFHPA2 OR DFHPA3
                    CONTINUE
      *
      *--- Teclas de funcion Legales
      *--- Teclas que requieren un procesado del mapa
      *
               WHEN EIBAID = DFHENTER
                 OR EIBAID = DFHPF1
                 OR EIBAID = DFHPF3
                 OR EIBAID = DFHPF12
                    PERFORM 1000-PROCESO-MAPA
      *
      *--- Teclas ilegales
      *--- Teclas no programadas que se detectaran en proceso de mapa
      *
               WHEN OTHER
                    PERFORM 1000-PROCESO-MAPA
      *
           END-EVALUATE
      *
      *--- Terminamos el proceso con Return TransId
      *--- Volvemos a enganchar esta misma transaccion
      *
           PERFORM 8200-RETORNO-TRANS.
      *
       0000-PROCESO-TAREA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Lectura valor idioma por PK                              ---
      *----------------------------------------------------------------
       110-LANGUAGE-PK.
      *
           EXEC SQL
                SELECT  VALOR
                   INTO
                        :TB-VALOR
                   FROM TABLAS
                   WHERE
                        TABLA  = 'LANGUAGE'
                    AND CLAVE1 = :TB-CLAVE1
           END-EXEC.
      *
       110-LANGUAGE-PK-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Procesa la pantalla segun opcion elegida en menu         ---
      *----------------------------------------------------------------
       1000-PROCESO-MAPA.
      *
      * Recuperamos el mapa desde el terminal
      *
           PERFORM 8400-RECIBE-MAPA
      *
           EVALUATE TRUE
      *
      *--- Enter: Validamos el mapa y si es correcto proceso Enter
      *
              WHEN EIBAID = DFHENTER
                  PERFORM 2000-ACCION-MAPA
      *
      *--- F3: Se sale del aplicativo (Exit)
      *
              WHEN EIBAID = DFHPF3
                   MOVE LOW-VALUE TO HOLAMPO
                   PERFORM 8100-ENVIA-MENSAJE
                   PERFORM 8900-RETORNO-CICS
           END-EVALUATE.
      *
           SET ENVIO-DATAONLY TO TRUE.
           PERFORM 8000-ENVIO-MAPA.
      *
       1000-PROCESO-MAPA-EXIT.
           EXIT.
      *
      *----------------------------------------------------------------
      *--- Validamos y procesamos el mapa                           ---
      *----------------------------------------------------------------
       2000-ACCION-MAPA.
           PERFORM 2100-VALIDA-CAMPOS.
           IF NOT ERROR-VALIDACION THEN
              PERFORM 2500-EJECUTA-ACCION
           END-IF.
       2000-ACCION-MAPA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Validamos campos obligatorios segun accion               ---
      *----------------------------------------------------------------
       2100-VALIDA-CAMPOS.
           MOVE ZERO TO SW-ERROR-VALIDACION.
           IF USUI EQUAL SPACES OR
              USUI EQUAL LOW-VALUES OR
              USUI EQUAL HIGH-VALUES THEN
              SET ERROR-VALIDACION TO TRUE
              MOVE WC-CUR-USU      TO CH-CURPOS
              MOVE 'USUOBL'        TO CL-CLAVE1
              PERFORM 20100-OBTENER-TEXTO
              MOVE CL-VALOR        TO MSGO
      * USUARIO es Obligatorio...
           END-IF.
       2100-VALIDA-CAMPOS-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Ejecutamos la accion                                     ---
      *----------------------------------------------------------------
       2500-EJECUTA-ACCION.
           MOVE SPACES TO MSGO.
           PERFORM 2510-CONSULTA.
           IF SQLCODE = +100 THEN
              MOVE SPACES TO PSWO
              MOVE 'USUINEX' TO CL-CLAVE1
              PERFORM 20100-OBTENER-TEXTO
              MOVE CL-VALOR  TO MSGO
      * El USUARIO NO Existe...
           END-IF.
       2500-EJECUTA-ACCION-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Consulta de usuario                                      ---
      *----------------------------------------------------------------
       2510-CONSULTA.
      *
           IF CH-CURPOS = WC-CUR-PSW THEN
              PERFORM 7100-VALIDAR-PASSWORD
           ELSE
              MOVE USUO TO US-USUARIO
              PERFORM 7000-USUARIO-PK
              IF SQLCODE = 0 THEN
                 IF PSWO EQUAL SPACES OR
                    PSWO EQUAL HIGH-VALUES OR
                    PSWO EQUAL LOW-VALUES THEN
                    MOVE DFHBMPRF   TO USUA
                    MOVE WC-CUR-PSW TO CH-CURPOS
                 ELSE
                    PERFORM 7100-VALIDAR-PASSWORD
                 END-IF
              END-IF
           END-IF.
       2510-CONSULTA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Leemos el idioma actual de la tabla                      ---
      *----------------------------------------------------------------
       2700-READ-LANGUAGE.
      *
           EXEC SQL
                SELECT VALOR
                  INTO :TB-VALOR
                  FROM TABLAS
                       WHERE TABLA  = 'LANGUAGE'
                         AND CLAVE1 = 'X'
                         AND CLAVE2 = 'X'
                         AND CLAVE3 = 'X'
           END-EXEC.
      *
           IF SQLCODE NOT = ZERO THEN
              PERFORM G999-ERROR-DB2
           ELSE
              MOVE TB-VALOR TO CL-IDIOMA
           END-IF.
      *
       2700-READ-LANGUAGE-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Obtener la fecha y la hora                               ---
      *----------------------------------------------------------------
       2710-ASKTIME.
            EXEC CICS
                 ASKTIME
                 ABSTIME(TSTAMP)
            END-EXEC.
            EXEC CICS
                 FORMATTIME
                 ABSTIME(TSTAMP)
                 MMDDYYYY(OUTDATE)
                 DATESEP('/')
                 TIME(OUTTIME)
                 TIMESEP(':')
            END-EXEC.
            MOVE CORR OUTDATER TO OUTDATEA.
            MOVE OUTDATEA      TO DATEO.
            MOVE OUTTIME       TO TIMEO.
       2710-ASKTIME-EXIT.
            EXIT.
      *----------------------------------------------------------------
      *--- Procesa la tecla F1                                      ---
      *---    Llamamos al programa de visualizacion de ayuda        ---
      *----------------------------------------------------------------
       5000-PROCESO-AYUDA.
      *
           MOVE CS-PGM-AYUDAOL             TO CH-XCTL
           MOVE WC-TRANSACCION             TO CH-TRANSACCION
           MOVE WC-TRANSACCION             TO CH-TRANS-RETORNO
           MOVE WC-PROGRAMA                TO CH-PROGRAMA-RETORNO
           PERFORM 8700-XCTL-PROGRAMA.
      *
       5000-PROCESO-AYUDA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Lectura tabla usuarios por PK                            ---
      *----------------------------------------------------------------
       7000-USUARIO-PK.
      *
           EXEC SQL
                SELECT  USUARIO
                      , NOMBRE
                      , PASSWORD
                      , CONECTADO
                   INTO
                       :US-USUARIO
                     , :US-NOMBRE
                     , :US-PASSWORD
                     , :US-CONECTADO
                   FROM USUARIOS
                   WHERE
                       USUARIO = :US-USUARIO
           END-EXEC.
      *
           IF SQLCODE NOT = 0 AND SQLCODE NOT = +100 THEN
              PERFORM G999-ERROR-DB2
           END-IF.
      *
       7000-USUARIO-PK-EXIT.
            EXIT.
      *----------------------------------------------------------------
      *--- Validar la password                                      ---
      *----------------------------------------------------------------
       7100-VALIDAR-PASSWORD.
            IF PSWO = US-PASSWORD THEN
      *--- Password correcta. Llamamos al men£.
               PERFORM 7400-EJECUCION-MENU
            ELSE
               SET ERROR-VALIDACION TO TRUE
               MOVE 'PSWERR'        TO CL-CLAVE1
               PERFORM 20100-OBTENER-TEXTO
               MOVE CL-VALOR        TO MSGO
      *        MOVE 'Password Incorrecta. Pruebe de nuevo...'
            END-IF.
       7100-VALIDAR-PASSWORD-EXIT.
            EXIT.
      *----------------------------------------------------------------
      *--- Llamamos al programa de menu HOLM                        ---
      *----------------------------------------------------------------
       7400-EJECUCION-MENU.
      *
           MOVE US-USUARIO                 TO US-USER.
           MOVE CS-PGM-MENU                TO CH-XCTL.
           MOVE WC-TRANSACCION             TO CH-TRANSACCION.
           MOVE WC-TRANSACCION             TO CH-TRANS-RETORNO.
           MOVE SPACES                     TO CH-PROGRAMA-RETORNO.
           PERFORM 8700-XCTL-PROGRAMA.
      *
       7400-EJECUCION-MENU-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Envia el mapa al terminal segun modo del switch          ---
      *----------------------------------------------------------------
       8000-ENVIO-MAPA.
      *
           EVALUATE TRUE
      *
      *--- Envio de mapa limpio
      *
               WHEN ENVIO-ERASE
      * Carga del t¡tulo del mapa
                   MOVE 60 TO RTC-CENTER-AREA
                   PERFORM 20000-TITULO-MAPA
      * Ajustar a la izquierda
                   PERFORM VARYING IND FROM 1 BY 1
                       UNTIL TITLEO(IND:1) NOT EQUAL SPACE
                   END-PERFORM
                   MOVE TITLEO                  TO RTC-OUTPUT-STRING
                   MOVE RTC-OUTPUT-STRING(IND:) TO TITLEO
      * Carga de los textos fijos del mapa
                   MOVE WC-TRANSACCION TO TB-CLAVE1
                   PERFORM 20200-OPEN-CURSOR-MAPS
                   PERFORM WITH TEST AFTER UNTIL SQLCODE = 100
                       PERFORM 20200-FETCH-CURSOR-MAPS
                       IF SQLCODE NOT = 100 AND
                          TB-CLAVE3 = CL-IDIOMA THEN
                          ADD 1 TO IND
                          IF TB-CLAVE2 = 'TX010' THEN
                             MOVE TB-VALOR TO TX010O
                          END-IF
                          IF TB-CLAVE2 = 'TX020' THEN
                             MOVE TB-VALOR TO TX020O
                          END-IF
                          IF TB-CLAVE2 = 'TX030' THEN
                             MOVE TB-VALOR TO TX030O
                          END-IF
                          IF TB-CLAVE2 = 'TX040' THEN
                             MOVE TB-VALOR TO TX040O
                          END-IF
                          IF TB-CLAVE2 = 'TX100' THEN
                             MOVE TB-VALOR TO TX100O
                          END-IF
                       END-IF
                   END-PERFORM
                   PERFORM 20200-CLOSE-CURSOR-MAPS
      *
                   EXEC CICS
                       SEND MAP    ('HOLAMP')
                            MAPSET ('HOLAMP')
                            FROM   (HOLAMPO)
                            CURSOR (CH-CURPOS)
                   END-EXEC
      *
      *--- Envio del mapa Solo datos
      *
               WHEN ENVIO-DATAONLY
      *
                   EXEC CICS
                       SEND MAP     ('HOLAMP')
                            MAPSET  ('HOLAMP')
                            FROM    (HOLAMPO)
                            CURSOR (CH-CURPOS)
                            DATAONLY
                   END-EXEC
      *
           END-EVALUATE.
      *
       8000-ENVIO-MAPA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Envia un mensaje al terminal del usuario (fin sesi¢n)    ---
      *----------------------------------------------------------------
       8100-ENVIA-MENSAJE.
      *
           MOVE 'HOLAEND'       TO CL-CLAVE1.
           PERFORM 20100-OBTENER-TEXTO.
           MOVE CL-VALOR        TO WL-FIN-SESION.
           EXEC CICS
              SEND TEXT FROM(WL-FIN-SESION)
                        ERASE
                        FREEKB
           END-EXEC.
      *
       8100-ENVIA-MENSAJE-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Devuelve el control a una transaccion                    ---
      *----------------------------------------------------------------
       8200-RETORNO-TRANS.
      *
           EXEC CICS
               RETURN
               TRANSID      (WA-TRANSACCION)
               COMMAREA     (CH-COMMAREA-HOL)
           END-EXEC.
      *
       8200-RETORNO-TRANS-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Recibe el mapa de la tarea                               ---
      *----------------------------------------------------------------
       8400-RECIBE-MAPA.
      *
           EXEC CICS
                RECEIVE
                MAP     ('HOLAMP')
                MAPSET  ('HOLAMP')
                INTO    (HOLAMPI)
           END-EXEC.
      *
           PERFORM 8410-MAYUSCULAS.
      *
       8400-RECIBE-MAPA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Pasa datos sensibles a mayusculas                        ---
      *----------------------------------------------------------------
       8410-MAYUSCULAS.
      *
           MOVE FUNCTION UPPER-CASE(USUI)    TO USUO.
      *
       8410-MAYUSCULAS-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Llama a otro programa CICS por LINK con Commarea         ---
      *----------------------------------------------------------------
       8500-LINK-PROGRAMA.
      *
           EXEC CICS
                LINK
                PROGRAM (CH-LINK)
                COMMAREA(CH-COMMAREA-HOL)
                LENGTH  (LENGTH OF CH-COMMAREA-HOL)
           END-EXEC.
      *
       8500-LINK-PROGRAMA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Commit de base de datos DB2 dede CICS                    ---
      *----------------------------------------------------------------
       8600-COMMIT.
      *
           EXEC CICS
               SYNCPOINT
           END-EXEC.
      *
       8600-COMMIT-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Cede el control a otro programa CICS                     ---
      *----------------------------------------------------------------
       8700-XCTL-PROGRAMA.
      *
           EXEC CICS
                SET TERMINAL(EIBTRMID) UCTRAN
           END-EXEC
      *
           EXEC CICS
               XCTL PROGRAM  (CH-XCTL)
                    COMMAREA (CH-COMMAREA-HOL)
                    RESP     (WA-RESPUESTA-CICS)
           END-EXEC.
      *
       8700-XCTL-PROGRAMA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- CEDF visualiza una traza - Quitar para produccion        ---
      *----------------------------------------------------------------
       8800-TRAZA.
      *
           EXEC CICS
               ENTER       TRACEID(0)
               FROM        (WA-TRAZA)
           END-EXEC.
      *
       8800-TRAZA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Devuelve el control a CICS - Salida total                ---
      *----------------------------------------------------------------
       8900-RETORNO-CICS.
      *
           EXEC CICS
                SET TERMINAL(EIBTRMID) UCTRAN
           END-EXEC
      *
           EXEC CICS
                RETURN
           END-EXEC.
      *
       8900-RETORNO-CICS-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- TXMUL ->
      *
      * Rutinas multiidioma
      *
           EXEC SQL
                INCLUDE PTXMUL
           END-EXEC.
      *
       999999-DB2-ERRORES SECTION.
      *--- TXMUL <-
      *--- DB2 Errores y otros                                      ---
      *----------------------------------------------------------------
           EXEC SQL
                INCLUDE PDDB2
           END-EXEC.
      *----------------------------------------------------------------
      *-------------------- FIN DE PROGRAMA  --------------------------
      *----------------------------------------------------------------
