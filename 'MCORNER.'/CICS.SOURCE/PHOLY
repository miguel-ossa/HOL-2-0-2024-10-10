      *****************************************************************
      *                 IDENTIFICATION     DIVISION                   *
      *****************************************************************
       IDENTIFICATION DIVISION.
      *
       PROGRAM-ID.      PHOLY.
       AUTHOR.          LUIS MARTINEZ.
      *
      *****************************************************************
      **  PROYECTO .........: HOL                                    **
      **  PROGRAMA .........: PHOLY                                  **
      **  VERSION ..........: 1.0                                    **
      **  TITULO ...........: ENVIO LISTADO DE HOLS A SPOOL          **
      **                                                             **
      **  TIPO .............:                                        **
      **     - LENGUAJE ...............: COBOL II                    **
      **     - ENTORNO ................: CICS                        **
      **     - BASE DE DATOS ..........: DB2                         **
      **                                                             **
      **  DESCRIPCION ......:                                        **
      **                                                             **
      **    - HOLY lee las colas creadas por la transaccion HOLL     **
      **      que a su vez es arrancada desde HOLS. La cola se crea  **
      **      a modo delistado de todos los registros quecumplen los **
      **      filtros y ordenacion de HOLS. La cola se llama         **
      **      HOLLxxxx donde xxxx es el c¢digo de terminal que ha    **
      **      lanzado la transaccion HOLL.                           **
      **                                                             **
      **    - HOLY lee la cola TS generada en HOLS y env¡a los       **
      **      registros a un rchivo de Spool al JES2.                **
      **      El archivo se genera en la clase de salida A.          **
      **      Si tienes una impresora definida en clase A, el listado**
      **      va directo a la impresora y si no, se queda en Spool   **
      **                                                             **
      **  LOG DE MODIFICACIONES:                                     **
      **  FECHA----- DESCRIPCION------------------------------ BUSCA **
      **  2024-08-18 Adaptaci¢n multiidioma (MOSSA)            TXMUL **
      **  2024-09-24 Multiusuario (MOSSA)                      USER  **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      *****************************************************************
       ENVIRONMENT DIVISION.
      *
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *
       DATA DIVISION.
      *----------------------------------------------------------------
       WORKING-STORAGE SECTION.
      *
      *--- Zona CICS --------------------------------------------------
      *
           COPY DFHAID.
           COPY DFHBMSCA.
           COPY CICSATTR.
           COPY DDCICS.
           COPY RESPCICS.
           COPY HOLYMP.
      *
      *--- Zona CICS fin ----------------------------------------------
      *
      *
      *--- Zona DB2 ---------------------------------------------------
      *
           EXEC SQL
                INCLUDE SQLCA
           END-EXEC
      *
           EXEC SQL
                INCLUDE DDDB2
           END-EXEC
      *--- TXMUL ->
           EXEC SQL
                INCLUDE TABLAS
           END-EXEC
      *--- TXMUL <-
      *--- Zona DB2 fin -----------------------------------------------
      *
       01  FILLER                          PIC X(19)
           VALUE '***START-WORKING***'.
      *
       01  WC-CONSTANTES.
           03  WC-PROGRAMA                 PIC X(8)  VALUE 'PHOLY'.
           03  WC-TRANSACCION              PIC X(4)  VALUE 'HOLY'.
           03  WC-RECURSO-HOL              PIC X(4)  VALUE 'HOL*'.
      *--- USER ->
      *    03  WC-LONGITUD-TITULO          PIC 99    VALUE 58.
      *--- USER <-
      *
       01  SW-SWITCHES.
      *
           03 SW-ENVIO-MAPA                PIC X    VALUE '0'.
              88 ENVIO-ERASE                        VALUE '1'.
              88 ENVIO-DATAONLY                     VALUE '2'.
      *
       01  WL-LITERALES.
           03 WL-FIN-SESION                PIC X(80) VALUE
           'La transacci¢n HOLY ha finalizado...'.
      *
      *--- Zona para CALLs --------------------------------------------
      *
           COPY LKCENTI.
      *
      *--- Zona para cola TS ------------------------------------------
      *
           COPY TSHOLL.
      *
      * Zona especial para CICS -> SPOOL
      *
       01  RESP                            PIC 9(8)   BINARY.
       01  RESP2                           PIC 9(8)   BINARY.
       01  TOKEN                           PIC X(8).
       01  OUTLEN                          PIC S9(8)  BINARY VALUE 133.
       77  OUTPRT                          PIC X(133).
       01  PARMSPTR                                   POINTER.
       01  PARMS-AREA.
           03  PARMSLEN                    PIC S9(8)  BINARY VALUE 8.
           03  PARMSINF                    PIC X(8)   VALUE SPACES.
           03  PARMADDR                               POINTER.
      *--- TXMUL ->
       77 IND                             PIC 99 VALUE ZERO.
      *--- TXMUL <-
      *
       01 FILLER                           PIC X(17)
           VALUE '***END-WORKING***'.
      *----------------------------------------------------------------
       LINKAGE SECTION.
      *
       01 DFHCOMMAREA                      PIC X(2048).
      *
      *================================================================
       PROCEDURE DIVISION.
      *----------------------------------------------------------------
      *--- Proceso principal                                        ---
      *----------------------------------------------------------------
       0000-PROCESO-TAREA.
      *
           EXEC SQL
                INCLUDE PDOPCIDB
           END-EXEC
      *
           MOVE WC-TRANSACCION             TO WA-TRANSACCION
      *
           IF EIBCALEN > 0
              MOVE DFHCOMMAREA             TO CH-COMMAREA-HOL
           END-IF
      *
      *--- Informamos el nombre de la cola TS (TRANS + TERM)
      *
           MOVE TH-TRN-LISTADO             TO LTS-TRANSACCION
           MOVE TH-TRN-VISUAL-LIST         TO LTR-TRANSACCION
           MOVE EIBTRMID                   TO LTS-TERMINAL
           MOVE EIBTRMID                   TO LTR-TERMINAL
      *
      *--- Evaluacion de Commarea y Teclas para identificar
      *--- los procesos que deban de ejecutarse
      *
           EVALUATE TRUE
      *
      *--- No hay Commarea
      *--- Se inicializa la Commarea y se envia mapa limpio
      *
               WHEN EIBCALEN = ZERO
                    MOVE LOW-VALUE         TO HOLYMPO
                    MOVE DFHRED            TO MSGC
      *--- TXMUL ->
                    MOVE 'HOLYARR'          TO CL-CLAVE1
                    PERFORM 20100-OBTENER-TEXTO
                    MOVE CL-VALOR          TO MSGO
      *             MOVE 'Para arrancar HOLY utiliza HOLM... F3'
      *               TO MSGO
      *--- TXMUL <-
                    SET ENVIO-ERASE        TO TRUE
                    PERFORM 8000-ENVIO-MAPA
      *
      *--- Artima¤a para forzar la vuelta
      *
               WHEN CH-Y-RETORNO = '99'
                    PERFORM 1200-PROCESO-VOLVER
      *
      *--- Hay Commarea
      *--- El programa ha podido ser arrancado por XCTL desde otro
      *--- programa. en este caso la transaccion actual ser  la del
      *--- programa que ha llamado a este hasta que se haga un
      *--- RETURN TRANSID
      *
               WHEN EIBCALEN > 0 AND EIBTRNID NOT = WC-TRANSACCION
                    MOVE LOW-VALUE         TO HOLYMPO
                    SET ENVIO-ERASE        TO TRUE
                    PERFORM 0100-POR-DEFECTO
                    PERFORM 8000-ENVIO-MAPA
      *
      *--- Borrado de pantalla  - Mensaje y retorno a CICS
      *--- F3 tambien puede terminar esta transaccion
      *
               WHEN EIBAID = DFHCLEAR
                    MOVE LOW-VALUE         TO HOLYMPO
                    PERFORM 8100-ENVIA-MENSAJE
                    PERFORM 8900-RETORNO-CICS
      *
      *--- Teclas de Att no hacemos nada
      *
               WHEN EIBAID = DFHPA1 OR DFHPA2 OR DFHPA3
                    CONTINUE
      *
      *--- Teclas de funcion Legales
      *--- Teclas que requieren un procesado del mapa
      *
               WHEN EIBAID = DFHENTER
                 OR EIBAID = DFHPF1
                 OR EIBAID = DFHPF3
                    PERFORM 1000-PROCESO-MAPA
      *
      *--- Teclas ilegales
      *--- Teclas no programadas que se detectaran en proceso de mapa
      *
               WHEN OTHER
                    PERFORM 1000-PROCESO-MAPA
      *
           END-EVALUATE
      *
      *--- Terminamos el proceso con Return TransId
      *--- Volvemos a enganchar esta misma transaccion
      *
           PERFORM 8200-RETORNO-TRANS.
      *
       0000-PROCESO-TAREA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Rellenamos el mapa con datos por defecto o iniciales     ---
      *--- Leemos la cola LTR resumen del £tlimo listado ejecutado  ---
      *----------------------------------------------------------------
       0100-POR-DEFECTO.
      *
           MOVE 1                          TO LTR-ITEM
           PERFORM 6000-ENQ-RECURSO
      *
           PERFORM 2100-LEER-ITEM-LTR
      *
           IF EIBRESP NOT = RESP-OK
              MOVE DFHRED               TO MSGC
      *--- TXMUL ->
              MOVE 'NLISPRIN'           TO CL-CLAVE1
              PERFORM 20100-OBTENER-TEXTO
              MOVE CL-VALOR             TO MSGO
      *       MOVE 'No hay ning£n lisado para imprimir...F3'
      *         TO MSGO
      *--- TXMUL <-
           END-IF
      *
           PERFORM 6100-DEQ-RECURSO.
      *
       0100-POR-DEFECTO-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Proceso del Mapa                                         ---
      *----------------------------------------------------------------
       1000-PROCESO-MAPA.
      *
      * Recuperamos el mapa desde el terminal
      *
           PERFORM 8400-RECIBE-MAPA
           MOVE SPACES                     TO MSGO
      *
           EVALUATE TRUE
      *
      *--- F1 : llamamos al programa de ayuda
      *
              WHEN EIBAID = DFHPF1
                  PERFORM 1100-PROCESO-AYUDA
      *
      *--- F3 : volvemos al menu principal
      *
              WHEN EIBAID = DFHPF3
                  PERFORM 1200-PROCESO-VOLVER
      *
      *--- Enter : Escribimos contenido de cola TS en Spool
      *
              WHEN EIBAID = DFHENTER
                  PERFORM 1400-PROCESO-LISTADO
      *
              WHEN OTHER
                  MOVE DFHRED              TO MSGC
      *--- TXMUL ->
                  MOVE 'KEYERR'            TO CL-CLAVE1
                  PERFORM 20100-OBTENER-TEXTO
                  MOVE CL-VALOR            TO MSGO
      *           MOVE 'Tecla err¢nea...'  TO MSGO
      *--- TXMUL <-
      *
           END-EVALUATE
      *
           SET ENVIO-DATAONLY              TO TRUE
           PERFORM 8000-ENVIO-MAPA.
      *
       1000-PROCESO-MAPA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Llamamos al programa Menu principal                      ---
      *----------------------------------------------------------------
       1100-PROCESO-AYUDA.
      *
           MOVE CS-PGM-AYUDAOL             TO CH-XCTL
           MOVE WC-TRANSACCION             TO CH-TRANSACCION
           MOVE WC-TRANSACCION             TO CH-TRANS-RETORNO
           MOVE WC-PROGRAMA                TO CH-PROGRAMA-RETORNO
           PERFORM 8700-XCTL-PROGRAMA.
      *
       1100-PROCESO-AYUDA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Llamamos al programa Menu principal                      ---
      *----------------------------------------------------------------
       1200-PROCESO-VOLVER.
      *
           MOVE CS-PGM-CLIENTES            TO CH-XCTL
           MOVE WC-TRANSACCION             TO CH-TRANSACCION
           MOVE WC-TRANSACCION             TO CH-TRANS-RETORNO
           MOVE WC-PROGRAMA                TO CH-PROGRAMA-RETORNO
           PERFORM 8700-XCTL-PROGRAMA.
      *
       1200-PROCESO-VOLVER-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Proceso de la p gina siguiente                           ---
      *----------------------------------------------------------------
       1400-PROCESO-LISTADO.
      *
      *--- Proceso inicial
      *
           IF CTRLI = 'OK'
              PERFORM 1200-PROCESO-VOLVER
           END-IF
      *
           PERFORM 6000-ENQ-RECURSO
           MOVE 1                          TO LTS-ITEM
      *
           PERFORM 2000-LEER-ITEM-LTS

           IF EIBRESP = RESP-OK
              PERFORM 3000-ABRE-SPOOL
      *
              PERFORM 1410-LEE-ESCRIBE
                 UNTIL EIBRESP NOT = RESP-OK
      *
              PERFORM 5000-CIERRA-SPOOL
              MOVE '00'                    TO CH-Y-RETORNO
              MOVE 'OK'                    TO CTRLO
              MOVE DFHYELLO                TO MSGC
      *--- TXMUL ->
              MOVE 'LISENSPO'              TO CL-CLAVE1
              PERFORM 20100-OBTENER-TEXTO
              MOVE CL-VALOR                TO MSGO
      *       MOVE 'OK - El listado est  en el Spool o Impresora... F3'
      *         TO MSGO
      *--- TXMUL <-
           ELSE
              MOVE DFHYELLO                TO MSGC
      *--- TXMUL ->
              MOVE 'NLISPRI3'              TO CL-CLAVE1
              PERFORM 20100-OBTENER-TEXTO
              MOVE CL-VALOR                TO MSGO
      *       MOVE 'Por favor pulsa F3. No hay listado para imprimir'
      *         TO MSGO
      *--- TXMUL <-
           END-IF
      *
           PERFORM 6100-DEQ-RECURSO.
      *
       1400-PROCESO-LISTADO-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Lee item de la cola y lo escribe en Spool                ---
      *----------------------------------------------------------------
       1410-LEE-ESCRIBE.
      *
              MOVE LTS-DETALLE             TO OUTPRT
              PERFORM 4000-ESCRIBE-SPOOL
              ADD 1                        TO LTS-ITEM
              PERFORM 2000-LEER-ITEM-LTS.
      *
       1410-LEE-ESCRIBE-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Leer un Item de la cola LTS                              ---
      *----------------------------------------------------------------
       2000-LEER-ITEM-LTS.
      *
           EXEC CICS
                READQ TS
                  QUEUE     (LTS-COLA)
                  INTO      (LTS-DATOS-COLA)
                  LENGTH (LENGTH OF LTS-DATOS-COLA)
                  ITEM      (LTS-ITEM)
                  NOHANDLE
           END-EXEC.
      *
       2000-LEER-ITEM-LTS-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Leer un Item de la cola LTR                              ---
      *----------------------------------------------------------------
       2100-LEER-ITEM-LTR.
      *
            EXEC CICS
                 READQ TS
                   QUEUE    (LTR-COLA)
                   INTO     (LTR-DATOS-COLA)
                   LENGTH (LENGTH OF LTR-DATOS-COLA)
                   ITEM     (LTR-ITEM)
                   NOHANDLE
            END-EXEC.
      *
       2100-LEER-ITEM-LTR-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Abre el archivo de escritura en el Spool                 ---
      *----------------------------------------------------------------
       3000-ABRE-SPOOL.
      *
           MOVE EIBTRMID                   TO PARMSINF
           MOVE 'HOL*'                     TO PARMSINF(5:)
      *
           SET PARMSPTR TO ADDRESS OF PARMS-AREA
           SET PARMADDR TO PARMSPTR
           SET PARMSPTR TO ADDRESS OF PARMADDR
      *
           EXEC CICS SPOOLOPEN OUTPUT
                TOKEN(TOKEN)
                USERID('*')
                NODE('*')
                CLASS('A')
                MCC
                RESP(RESP)
                RESP2(RESP2)
           END-EXEC
      *
           IF EIBRESP NOT = RESP-OK
              MOVE DFHRED              TO MSGC
              MOVE 'Error de apertura del Spool...'
                TO MSGO
           END-IF.
      *
       3000-ABRE-SPOOL-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Escribe una l¡nea en el Spool                            ---
      *----------------------------------------------------------------
       4000-ESCRIBE-SPOOL.
      *
           IF LTS-CONTROL-CARRO = '0'
              EXEC CICS SPOOLWRITE
                   FROM(OUTPRT)
                   NOHANDLE
                   FLENGTH(OUTLEN)
                   PAGE
                   TOKEN(TOKEN)
              END-EXEC
           ELSE
              EXEC CICS SPOOLWRITE
                   FROM(OUTPRT)
                   NOHANDLE
                   FLENGTH(OUTLEN)
                   LINE
                   TOKEN(TOKEN)
              END-EXEC
           END-IF.
      *
           IF EIBRESP NOT = RESP-OK
              MOVE DFHRED              TO MSGC
      *--- TXMUL ->
              MOVE 'ERRSPOOL'          TO CL-CLAVE1
              PERFORM 20100-OBTENER-TEXTO
              MOVE CL-VALOR            TO MSGO
      *       MOVE 'Error de escritura en Spool...'
      *         TO MSGO
      *--- TXMUL <-
           END-IF.
      *
       4000-ESCRIBE-SPOOL-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Cierra el archivo de Spool                               ---
      *----------------------------------------------------------------
       5000-CIERRA-SPOOL.
      *
           EXEC CICS SPOOLCLOSE
                TOKEN(TOKEN)
                NOHANDLE
           END-EXEC
      *
           IF EIBRESP NOT = RESP-OK
              MOVE DFHRED              TO MSGC
      *--- TXMUL ->
              MOVE 'ERRCSPOO'          TO CL-CLAVE1
              PERFORM 20100-OBTENER-TEXTO
              MOVE CL-VALOR            TO MSGO
      *       MOVE 'Error de cierre de Spool...'
      *         TO MSGO
      *--- TXMUL <-
           END-IF.
      *
       5000-CIERRA-SPOOL-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Encola recurso comun                                     ---
      *----------------------------------------------------------------
       6000-ENQ-RECURSO.
      *
           EXEC CICS
                ENQ
                RESOURCE   (WC-RECURSO-HOL)
                LENGTH     (4)
           END-EXEC.
      *
       6000-ENQ-RECURSO-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Desencola recurso comun                                  ---
      *----------------------------------------------------------------
       6100-DEQ-RECURSO.
      *
           EXEC CICS
                DEQ
                RESOURCE   (WC-RECURSO-HOL)
                LENGTH     (4)
           END-EXEC.
      *
       6100-DEQ-RECURSO-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Envia el mapa al terminal sergun modo del switch         ---
      *----------------------------------------------------------------
       8000-ENVIO-MAPA.
      *
           MOVE +771                       TO CH-CURPOS
      *
           EVALUATE TRUE
      *
      *--- Envio de mapa limpio
      *
               WHEN ENVIO-ERASE
      *
      *--- TXMUL ->
                   MOVE 50 TO RTC-CENTER-AREA
                   PERFORM 20000-TITULO-MAPA
      * Carga de los textos fijos del mapa
                   MOVE WC-TRANSACCION TO TB-CLAVE1
                   PERFORM 20200-OPEN-CURSOR-MAPS
                   PERFORM WITH TEST AFTER UNTIL SQLCODE = 100
                       PERFORM 20200-FETCH-CURSOR-MAPS
                       IF SQLCODE NOT = 100 AND
                          TB-CLAVE3 = CL-IDIOMA THEN
                          ADD 1 TO IND
                          EVALUATE IND
                             WHEN 1
                                MOVE TB-VALOR TO TX10O
                             WHEN 2
                                MOVE TB-VALOR TO TX20O
                             WHEN 3
                                MOVE TB-VALOR TO TX30O
                          END-EVALUATE
                       END-IF
                   END-PERFORM
                   PERFORM 20200-CLOSE-CURSOR-MAPS
      *--- TXMUL <-
                   IF EIBCALEN = 0
                      MOVE '99'            TO CH-Y-RETORNO
                   END-IF
      *
                   EXEC CICS
                       SEND MAP    ('HOLYMP')
                            MAPSET ('HOLYMP')
                            FROM   (HOLYMPO)
                            CURSOR (CH-CURPOS)
                            ERASE
                   END-EXEC
      *
      *--- Envio del mapa Solo datos
      *
               WHEN ENVIO-DATAONLY
      *
                   EXEC CICS
                       SEND MAP     ('HOLYMP')
                            MAPSET  ('HOLYMP')
                            FROM    (HOLYMPO)
                            CURSOR  (CH-CURPOS)
                            DATAONLY
                   END-EXEC
      *
           END-EVALUATE.
      *
       8000-ENVIO-MAPA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Envia un mensaje al terminal del usuario (fin sesion)    ---
      *----------------------------------------------------------------
       8100-ENVIA-MENSAJE.
      *
      *--- TXMUL ->
           MOVE 'HOLYEND'                  TO CL-CLAVE1.
           PERFORM 20100-OBTENER-TEXTO.
           MOVE CL-VALOR                   TO WL-FIN-SESION.
      * 'La transaccion HOLY ha finalizado...'
           EXEC CICS
               SEND TEXT FROM(WL-FIN-SESION)
                         ERASE
                         FREEKB
           END-EXEC.
      *
       8100-ENVIA-MENSAJE-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Devuelve el control a una transaccion                    ---
      *----------------------------------------------------------------
       8200-RETORNO-TRANS.
      *
           EXEC CICS
                SET TERMINAL(EIBTRMID) TRANIDONLY
           END-EXEC
      *
           EXEC CICS
               RETURN
               TRANSID      (WA-TRANSACCION)
               COMMAREA     (CH-COMMAREA-HOL)
           END-EXEC.
      *
       8200-RETORNO-TRANS-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Recibe el mapa de la tarea                               ---
      *----------------------------------------------------------------
       8400-RECIBE-MAPA.
      *
           EXEC CICS
                RECEIVE
                MAP     ('HOLYMP')
                MAPSET  ('HOLYMP')
                INTO    (HOLYMPI)
           END-EXEC.
      *
       8400-RECIBE-MAPA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Llama a otro programa CICS por LINK con Commarea         ---
      *----------------------------------------------------------------
       8500-LINK-PROGRAMA.
      *
           EXEC CICS
                LINK
                PROGRAM (CH-LINK)
                COMMAREA(CH-COMMAREA-HOL)
                LENGTH  (LENGTH OF CH-COMMAREA-HOL)
           END-EXEC.
      *
       8500-LINK-PROGRAMA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Cede el control a otro programa CICS                     ---
      *----------------------------------------------------------------
       8700-XCTL-PROGRAMA.
      *
           EXEC CICS
                SET TERMINAL(EIBTRMID) UCTRAN
           END-EXEC
      *
           EXEC CICS
               XCTL PROGRAM  (CH-XCTL)
                    COMMAREA (CH-COMMAREA-HOL)
                    RESP     (WA-RESPUESTA-CICS)
           END-EXEC.
      *
       8700-XCTL-PROGRAMA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- CEDF visualiza una traza - Quitar para produccion        ---
      *----------------------------------------------------------------
       8800-TRAZA.
      *
           EXEC CICS
               ENTER       TRACEID(0)
               FROM        (WA-TRAZA)
           END-EXEC.
      *
       8800-TRAZA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Devuelve el control a CICS - Salida total                ---
      *----------------------------------------------------------------
       8900-RETORNO-CICS.
      *
           EXEC CICS
                SET TERMINAL(EIBTRMID) UCTRAN
           END-EXEC
      *
           EXEC CICS
                RETURN
           END-EXEC.
      *
       8900-RETORNO-CICS-EXIT.
           EXIT.
      *--- TXMUL ->
      *
      * Rutinas multiidioma
      *
           EXEC SQL
                INCLUDE PTXMUL
           END-EXEC.
      *
       999999-DB2-ERRORES SECTION.
      *--- TXMUL <-
      *----------------------------------------------------------------
      *--- DB2 Errores y otros                                      ---
      *----------------------------------------------------------------
           EXEC SQL
                INCLUDE PDDB2
           END-EXEC.
      *----------------------------------------------------------------
      *-------------------- FIN DE PROGRAMA  --------------------------
      *----------------------------------------------------------------
