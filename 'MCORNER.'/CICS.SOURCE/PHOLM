      *****************************************************************
      *                 IDENTIFICATION     DIVISION                   *
      *****************************************************************
       IDENTIFICATION DIVISION.
      *
       PROGRAM-ID.      PHOLM.
       AUTHOR.          MIGUEL DE LA OSSA.
      *
      *****************************************************************
      **  PROYECTO .........: HOL                                    **
      **  PROGRAMA .........: PHOLM                                  **
      **  VERSION ..........: 1.1                                    **
      **  TITULO ...........: MENU GENERAL                           **
      **                                                             **
      **  TIPO .............:                                        **
      **     - LENGUAJE ...............: COBOL II                    **
      **     - ENTORNO ................: CICS                        **
      **     - BASE DE DATOS ..........: DB2                         **
      **                                                             **
      **  DESCRIPCION ......:                                        **
      **                                                             **
      **    - Menu general de la aplicaci¢n                          **
      **      Dispondr  de varias opciones relacionadas con          **
      **      la gesti¢n ficticia de una empres.                     **
      **                                                             **
      **      Se lanzar  con la transacci¢n HOLA, preferentemente.   **
      **                                                             **
      **  LOG DE MODIFICACIONES:                                     **
      **  FECHA----- DESCRIPCION------------------------------ BUSCA **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      **                                                             **
      *****************************************************************
       ENVIRONMENT DIVISION.
      *
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *
       DATA DIVISION.
      *----------------------------------------------------------------
       WORKING-STORAGE SECTION.
      *
      *--- Zona CICS --------------------------------------------------
      *
       COPY DFHAID.
       COPY DFHBMSCA.
       COPY HOLMMP.
       COPY DDCICS.
      *
      *--- Zona CICS fin ----------------------------------------------
      *
      *--- Zona DB2 ---------------------------------------------------
      *
           EXEC SQL
                INCLUDE SQLCA
           END-EXEC
      *
           EXEC SQL
                INCLUDE DDDB2
           END-EXEC
      *--- TXMUL ->
           EXEC SQL
                INCLUDE TABLAS
           END-EXEC
      *--- TXMUL <-
      *--- Zona DB2 fin -----------------------------------------------
      *
       01  FILLER                          PIC X(19)
           VALUE '***START-WORKING***'.
      *
       01  WC-CONSTANTES.
           03  WC-PROGRAMA                 PIC X(8)  VALUE 'PHOLM'.
           03  WC-TRANSACCION              PIC X(4)  VALUE 'HOLM'.
           03  WC-MAP                      PIC X(8)  VALUE 'HOLMMP'.
           03  WC-MAPSET                   PIC X(8)  VALUE 'HOLMMP'.
           03  WC-CURPOS                   PIC S9(4) VALUE +992.
      *
           03  WC-CUR-CLIENTES             PIC S9(4) COMP VALUE +501.
           03  WC-CUR-PROVEEDORES          PIC S9(4) COMP VALUE +581.
           03  WC-CUR-ALMACEN              PIC S9(4) COMP VALUE +661.
           03  WC-CUR-PEDIDOS              PIC S9(4) COMP VALUE +741.
           03  WC-CUR-FACTURACION          PIC S9(4) COMP VALUE +821.
           03  WC-CUR-CONTABILIDAD         PIC S9(4) COMP VALUE +901.
           03  WC-CUR-ESTADISTICA          PIC S9(4) COMP VALUE +981.
           03  WC-CUR-CONFIGURACIONES      PIC S9(4) COMP VALUE +1061.
           03  WC-CUR-ADMINISTRACION       PIC S9(4) COMP VALUE +1141.
           03  WC-CUR-OPCION               PIC S9(4) COMP VALUE +1253.
      *
       01  SW-SWITCHES.
           03 SW-ENVIO-MAPA                PIC X    VALUE '0'.
              88 ENVIO-ERASE                        VALUE '1'.
              88 ENVIO-DATAONLY                     VALUE '2'.
      *
           03 SW-ERROR-VALIDACION          PIC 999  VALUE 0.
              88 ERROR-VALIDACION                   VALUE 1.
      *
       01  WL-LITERALES.
           03  WL-FIN-SESION               PIC X(80) VALUE
               'La transaccion HOLM ha finalizado...'.
      *TEMPORAL
      *77  DCURSOR                         PIC ZZZ9.
      *
      *--- Zona para CALLs --------------------------------------------
      *
           COPY LKCENTI.
      *
       01  WX-VARIABLES.
           03  WX-NUM                      PIC S9(4) VALUE 0.
           03  IND                         PIC 99    VALUE 0.
      *
       01 FILLER                           PIC X(17)
           VALUE '***END-WORKING***'.
      *----------------------------------------------------------------
       LINKAGE SECTION.
      *
       01 DFHCOMMAREA                      PIC X(2048).
      *
      *================================================================
       PROCEDURE DIVISION.
      *----------------------------------------------------------------
      *--- Proceso principal                                        ---
      *----------------------------------------------------------------
       0000-PROCESO-TAREA.
      *
           EXEC SQL
                INCLUDE PDOPCIDB
           END-EXEC
      *
           MOVE WC-TRANSACCION              TO WA-TRANSACCION
      *
           IF EIBCALEN > 0
              MOVE DFHCOMMAREA              TO CH-COMMAREA-HOL
           END-IF.
      *
      *--- Evaluacion de Commarea y Teclas para identificar
      *--- los procesos que deban de ejecutarse
      *
           EVALUATE TRUE
      *
      *--- No hay Commarea
      *--- Se inicializa la Commarea y se envia mapa limpio
      *
               WHEN EIBCALEN = ZERO
      *--- USER ->
                    IF US-USER EQUAL SPACES THEN
                       PERFORM 7400-USER-LOGIN
                    ELSE
                       MOVE LOW-VALUE          TO HOLMMPO
                       INITIALIZE CH-COMUN
                       PERFORM 2700-READ-LANGUAGE
                       SET ENVIO-ERASE         TO TRUE
                       PERFORM 8000-ENVIO-MAPA
                    END-IF
      *--- USER <-
      *
      *--- Hay Commarea
      *--- El programa ha podido ser arrancado por XCTL desde otro
      *--- programa con XCTL como retorno al actual. En este caso
      *--- el campo ch-programa-retorno contiene algun valor.
      *--- En este caso inicializamos la Commarea y eviamos mapa limpio
      *
               WHEN EIBCALEN > ZERO AND CH-TRANS-RETORNO NOT = SPACES
      *TEMPORAL
                    MOVE 'PHOLM' TO WA-TRAZA
                    PERFORM 8800-TRAZA
      *
                    MOVE LOW-VALUE          TO HOLMMPO
                    INITIALIZE CH-COMUN
                    PERFORM 2700-READ-LANGUAGE
                    SET ENVIO-ERASE         TO TRUE
                    PERFORM 8000-ENVIO-MAPA
      *
      *--- Borrado de pantalla  - Mensaje y retorno a CICS
      *--- F3 tambien puede terminar esta transaccion
      *
               WHEN EIBAID = DFHCLEAR OR EIBAID = DFHPF3
                    MOVE LOW-VALUE          TO HOLMMPO
                    PERFORM 8100-ENVIA-MENSAJE
                    PERFORM 8900-RETORNO-CICS
      *
      *--- Teclas de Att no hacemos nada
      *
               WHEN EIBAID = DFHPA1 OR DFHPA2 OR DFHPA3
                    CONTINUE
      *
      *--- Teclas de funcion Legales
      *--- Teclas que requieren un procesado del mapa
      *
               WHEN EIBAID = DFHENTER
                 OR EIBAID = DFHPF1
                 OR EIBAID = DFHPF12
                    PERFORM 1000-PROCESO-MAPA
      *
      *--- Teclas ilegales
      *--- Teclas no programadas que se detectaran en proceso de mapa
      *
               WHEN OTHER
                    PERFORM 1000-PROCESO-MAPA
      *
           END-EVALUATE
      *
      *--- Terminamos el proceso con Return TransId
      *--- Volvemos a enganchar esta misma transaccion
      *
           PERFORM 8200-RETORNO-TRANS.
      *
       0000-PROCESO-TAREA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Procesa la pantalla segun tecla elegida por el usuario   ---
      *----------------------------------------------------------------
       1000-PROCESO-MAPA.
      *
      * Recuperamos el mapa desde el terminal
      *
           PERFORM 8400-RECIBE-MAPA
      *
           EVALUATE TRUE
      *
      *--- Enter: Validamos el mapa y si es correcto proceso Enter
      *
              WHEN EIBAID = DFHENTER
                   PERFORM 1100-VALIDA-MAPA
      *
      *
      *--- F1: Se llama al programa de ayuda online
      *
              WHEN EIBAID = DFHPF1
                   PERFORM 5000-PROCESO-PF1
      *
      *-- Otras teclas: Se avisa con mensaje en pantalla y mapa limpio
      *
              WHEN OTHER
      *--- TXMUL ->
                   MOVE 'KEYERR'           TO CL-CLAVE1
                   PERFORM 20100-OBTENER-TEXTO
      *            MOVE 'Tecla erronea...' TO MSGO
                   MOVE CL-VALOR           TO MSGO
      *--- TXMUL <-
      *
           END-EVALUATE.
      *
           SET ENVIO-DATAONLY              TO TRUE
           PERFORM 8000-ENVIO-MAPA.
      *
       1000-PROCESO-MAPA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Valida los datos del mapa. Identifica la opcion          ---
      *----------------------------------------------------------------
       1100-VALIDA-MAPA.
      *
           MOVE SPACES                     TO CH-M-OPCION  CH-X-ACCION
           MOVE EIBCPOSN                   TO CH-CURPOS
           MOVE 0                          TO SW-ERROR-VALIDACION
      *
      *--- Prioridad al valor de la opcion en pantalla
      *
           IF CH-X-ACCION = SPACES             AND
              OPCI = 1
              PERFORM 1110-CLIENTES
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              OPCI = 2
              PERFORM 1120-PROVEEDORES
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              OPCI = 3
              PERFORM 1130-ALMACEN
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              OPCI = 4
              PERFORM 1140-PEDIDOS
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              OPCI = 5
              PERFORM 1150-FACTURACION
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              OPCI = 6
              PERFORM 1160-CONTABILIDAD
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              OPCI = 7
              PERFORM 1170-ESTADISTICA
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              OPCI = 8
              PERFORM 1180-CONFIGURACIONES
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              OPCI = 9
              PERFORM 1190-ADMINISTRACION
           END-IF.
      *
           IF OPCI < 1                         AND
              (OPCI NOT = SPACES               AND
               OPCI NOT = LOW-VALUES           AND
               OPCI NOT = HIGH-VALUES)
              SET ERROR-VALIDACION       TO TRUE
              MOVE 'X'                   TO CH-X-ACCION  CH-M-OPCION
              MOVE 'OPCERR'              TO CL-CLAVE1
              PERFORM 20100-OBTENER-TEXTO
              MOVE CL-VALOR              TO MSGO
      *       MOVE 'Opci¢n seleccionada incorrecta...' TO MSGO
           END-IF.
      *
      *TEMPORAL
      *    MOVE CH-CURPOS TO DCURSOR.
      *    MOVE DCURSOR TO WA-TRAZA.
      *    PERFORM 8800-TRAZA.
      *
           IF CH-X-ACCION = SPACES             AND
              CH-CURPOS = WC-CUR-CLIENTES
              PERFORM 1110-CLIENTES
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              CUR1I NOT = SPACES               AND
              CUR1I NOT = HIGH-VALUES          AND
              CUR1I NOT = LOW-VALUES
              PERFORM 1110-CLIENTES
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              CH-CURPOS = WC-CUR-PROVEEDORES
              PERFORM 1120-PROVEEDORES
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              CUR2I NOT = SPACES               AND
              CUR2I NOT = HIGH-VALUES          AND
              CUR2I NOT = LOW-VALUES
              PERFORM 1120-PROVEEDORES
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              CH-CURPOS = WC-CUR-ALMACEN
              PERFORM 1130-ALMACEN
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              CUR3I NOT = SPACES               AND
              CUR3I NOT = HIGH-VALUES          AND
              CUR3I NOT = LOW-VALUES
              PERFORM 1130-ALMACEN
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              CH-CURPOS = WC-CUR-PEDIDOS
              PERFORM 1140-PEDIDOS
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              CUR4I NOT = SPACES               AND
              CUR4I NOT = HIGH-VALUES          AND
              CUR4I NOT = LOW-VALUES
              PERFORM 1140-PEDIDOS
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              CH-CURPOS = WC-CUR-FACTURACION
              PERFORM 1150-FACTURACION
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              CUR5I NOT = SPACES               AND
              CUR5I NOT = HIGH-VALUES          AND
              CUR5I NOT = LOW-VALUES
              PERFORM 1150-FACTURACION
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              CH-CURPOS = WC-CUR-CONTABILIDAD
              PERFORM 1160-CONTABILIDAD
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              CUR6I NOT = SPACES               AND
              CUR6I NOT = HIGH-VALUES          AND
              CUR6I NOT = LOW-VALUES
              PERFORM 1160-CONTABILIDAD
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              CH-CURPOS = WC-CUR-ESTADISTICA
              PERFORM 1170-ESTADISTICA
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              CUR7I NOT = SPACES               AND
              CUR7I NOT = HIGH-VALUES          AND
              CUR7I NOT = LOW-VALUES
              PERFORM 1170-ESTADISTICA
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              CH-CURPOS = WC-CUR-CONFIGURACIONES
              PERFORM 1180-CONFIGURACIONES
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              CUR8I NOT = SPACES               AND
              CUR8I NOT = HIGH-VALUES          AND
              CUR8I NOT = LOW-VALUES
              PERFORM 1180-CONFIGURACIONES
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              CH-CURPOS = WC-CUR-ADMINISTRACION
              PERFORM 1190-ADMINISTRACION
           END-IF.
      *
           IF CH-X-ACCION = SPACES             AND
              CUR9I NOT = SPACES               AND
              CUR9I NOT = HIGH-VALUES          AND
              CUR9I NOT = LOW-VALUES
              PERFORM 1190-ADMINISTRACION
           END-IF.
      *TEMPORAL
      *    MOVE CH-M-OPCION TO WA-TRAZA.
      *    PERFORM 8800-TRAZA.
      *
           IF NOT ERROR-VALIDACION AND CH-M-OPCION = 'C'
              PERFORM 2000-CLIENTES
           END-IF.
      *
           IF NOT ERROR-VALIDACION AND CH-M-OPCION = 'I'
              PERFORM 7300-ADMINISTRACION
           END-IF.
      *
      *
           IF CH-X-ACCION = SPACES
              SET ERROR-VALIDACION         TO TRUE
              MOVE 'OPCDESC'               TO CL-CLAVE1
              PERFORM 20100-OBTENER-TEXTO
              MOVE CL-VALOR                TO MSGO
      *       MOVE 'No se ha podido determinar la opcion'
      *         TO MSGO
           END-IF.
      *
       1100-VALIDA-MAPA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Opcion Clientes                                          ---
      *----------------------------------------------------------------
       1110-CLIENTES.
      *
           MOVE 'C'                        TO CH-M-OPCION  CH-X-ACCION.
           MOVE '1'                        TO OPCO.
           MOVE SPACES                     TO MSGO.
      *
       1110-CONSULTA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- PROVEEDORES - Opcion todav¡a no habilitada               ---
      *----------------------------------------------------------------
       1120-PROVEEDORES.
      *
           SET ERROR-VALIDACION         TO TRUE.
           MOVE 'OPCINVA'               TO CL-CLAVE1.
           PERFORM 20100-OBTENER-TEXTO.
           MOVE '2'                        TO OPCO.
           MOVE CL-VALOR                TO MSGO.
      * 'Opci¢n todav¡a no Habilitada.'
      *
       1120-PROVEEDORES-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- ALMACEN - Opcion todav¡a no habilitada                   ---
      *----------------------------------------------------------------
       1130-ALMACEN.
      *
           SET ERROR-VALIDACION         TO TRUE.
           MOVE 'OPCINVA'               TO CL-CLAVE1.
           PERFORM 20100-OBTENER-TEXTO.
           MOVE '3'                        TO OPCO.
           MOVE CL-VALOR                TO MSGO.
      * 'Opci¢n todav¡a no Habilitada.'
      *
       1130-ALMACEN-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- PEDIDOS - Opcion todav¡a no habilitada                   ---
      *----------------------------------------------------------------
       1140-PEDIDOS.
      *
           SET ERROR-VALIDACION         TO TRUE.
           MOVE 'OPCINVA'               TO CL-CLAVE1.
           PERFORM 20100-OBTENER-TEXTO.
           MOVE '4'                        TO OPCO.
           MOVE CL-VALOR                TO MSGO.
      * 'Opci¢n todav¡a no Habilitada.'
      *
       1140-PEDIDOS-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- FACTURACION - Opcion todav¡a no habilitada               ---
      *----------------------------------------------------------------
       1150-FACTURACION.
      *
           SET ERROR-VALIDACION         TO TRUE.
           MOVE 'OPCINVA'               TO CL-CLAVE1.
           PERFORM 20100-OBTENER-TEXTO.
           MOVE '5'                        TO OPCO.
           MOVE CL-VALOR                TO MSGO.
      * 'Opci¢n todav¡a no Habilitada.'
      *
       1150-FACTURACION-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- CONTABILIDAD - Opcion todav¡a no habilitada              ---
      *----------------------------------------------------------------
       1160-CONTABILIDAD.
      *
           SET ERROR-VALIDACION         TO TRUE.
           MOVE 'OPCINVA'               TO CL-CLAVE1.
           PERFORM 20100-OBTENER-TEXTO.
           MOVE '6'                        TO OPCO.
           MOVE CL-VALOR                TO MSGO.
      * 'Opci¢n todav¡a no Habilitada.'
      *
       1160-CONTABILIDAD-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- ESTADISTICA - Opcion todav¡a no habilitada               ---
      *----------------------------------------------------------------
       1170-ESTADISTICA.
      *
           SET ERROR-VALIDACION         TO TRUE.
           MOVE 'OPCINVA'               TO CL-CLAVE1.
           PERFORM 20100-OBTENER-TEXTO.
           MOVE '7'                        TO OPCO.
           MOVE CL-VALOR                TO MSGO.
      * 'Opci¢n todav¡a no Habilitada.'
      *
       1170-ESTADISTICA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- CONFIGURACIONES - Opcion todav¡a no habilitada           ---
      *----------------------------------------------------------------
       1180-CONFIGURACIONES.
      *
           SET ERROR-VALIDACION         TO TRUE.
           MOVE 'OPCINVA'               TO CL-CLAVE1.
           PERFORM 20100-OBTENER-TEXTO.
           MOVE '8'                        TO OPCO.
           MOVE CL-VALOR                TO MSGO.
      * 'Opci¢n todav¡a no Habilitada.'
      *
       1180-CONFIGURACIONES-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- CB001 Opcion de Administraci¢n                           ---
      *----------------------------------------------------------------
       1190-ADMINISTRACION.
      *
           MOVE 'I'                        TO CH-M-OPCION  CH-X-ACCION.
           MOVE '9'                        TO OPCO.
           MOVE SPACES                     TO MSGO.
           MOVE EIBTRMID                   TO CH-TERMINAL.
      *
       1190-ADMINISTRACION-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Llamamos al men£ de Clientes                             ---
      *----------------------------------------------------------------
       2000-CLIENTES.
      *
           MOVE CS-PGM-CLIENTES            TO CH-XCTL.
           MOVE WC-TRANSACCION             TO CH-TRANSACCION.
           MOVE WC-TRANSACCION             TO CH-TRANS-RETORNO.
           MOVE WC-PROGRAMA                TO CH-PROGRAMA-RETORNO.
           PERFORM 8700-XCTL-PROGRAMA.
      *
       2000-MANTENIMIENTO-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Leemos el idioma actual de la tabla                      ---
      *----------------------------------------------------------------
       2700-READ-LANGUAGE.
      *
           EXEC SQL
                SELECT VALOR
                  INTO :TB-VALOR
                  FROM TABLAS
                       WHERE TABLA  = 'LANGUAGE'
                         AND CLAVE1 = 'X'
                         AND CLAVE2 = 'X'
                         AND CLAVE3 = 'X'
           END-EXEC.
      *
           IF SQLCODE NOT = ZERO THEN
              PERFORM G999-ERROR-DB2
           ELSE
              MOVE TB-VALOR  TO CL-IDIOMA
      *       MOVE CL-IDIOMA TO WA-TRAZA
      *       PERFORM 8800-TRAZA
           END-IF.
      *
       2700-READ-LANGUAGE-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Procesa la tecla F1                                      ---
      *---    Llamamos al programa de visualizacion de ayuda        ---
      *----------------------------------------------------------------
       5000-PROCESO-PF1.
      *
           MOVE CS-PGM-AYUDAOL             TO CH-XCTL
           MOVE WC-TRANSACCION             TO CH-TRANSACCION
           MOVE WC-TRANSACCION             TO CH-TRANS-RETORNO
           MOVE WC-PROGRAMA                TO CH-PROGRAMA-RETORNO
           PERFORM 8700-XCTL-PROGRAMA.
      *
       5000-PROCESO-PF1-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Procesa la opci¢n 9. Administraci¢n                      ---
      *----------------------------------------------------------------
       7300-ADMINISTRACION.
      *
           MOVE CS-PGM-ADMINISTRACION      TO CH-XCTL
           MOVE WC-TRANSACCION             TO CH-TRANSACCION
           MOVE WC-TRANSACCION             TO CH-TRANS-RETORNO
           MOVE WC-PROGRAMA                TO CH-PROGRAMA-RETORNO
           PERFORM 8700-XCTL-PROGRAMA.
      *
       7300-ADMINISTRACION-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Llamamos al programa de login                            ---
      *----------------------------------------------------------------
       7400-USER-LOGIN.
      *
           MOVE CS-PGM-USER                TO CH-XCTL.
           MOVE WC-TRANSACCION             TO CH-TRANSACCION.
           MOVE WC-TRANSACCION             TO CH-TRANS-RETORNO.
           MOVE WC-PROGRAMA                TO CH-PROGRAMA-RETORNO.
           PERFORM 8700-XCTL-PROGRAMA.
      *
       7400-USER-LOGIN-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Envia el mapa al terminal sergun modo del switch         ---
      *----------------------------------------------------------------
       8000-ENVIO-MAPA.
      *
           EVALUATE TRUE
      *
      *--- Envio de mapa limpio
      *
               WHEN ENVIO-ERASE
      *
                   MOVE 50 TO RTC-CENTER-AREA
                   PERFORM 20000-TITULO-MAPA
      * Carga de los textos fijos del mapa
                   MOVE WC-TRANSACCION TO TB-CLAVE1
                   PERFORM 20200-OPEN-CURSOR-MAPS
                   PERFORM WITH TEST AFTER UNTIL SQLCODE = 100
                       PERFORM 20200-FETCH-CURSOR-MAPS
                       IF SQLCODE NOT = 100 AND
                          TB-CLAVE3 = CL-IDIOMA THEN
                          ADD 1 TO IND
                          EVALUATE IND
                             WHEN 1
                                MOVE TB-VALOR TO TX10O
                             WHEN 2
                                MOVE TB-VALOR TO TX20O
                             WHEN 3
                                MOVE TB-VALOR TO TX30O
                             WHEN 4
                                MOVE TB-VALOR TO TX40O
                             WHEN 5
                                MOVE TB-VALOR TO TX50O
                             WHEN 6
                                MOVE TB-VALOR TO TX60O
                             WHEN 7
                                MOVE TB-VALOR TO TX70O
                             WHEN 8
                                MOVE TB-VALOR TO TX80O
                             WHEN 9
                                MOVE TB-VALOR TO TX90O
                             WHEN 10
                                MOVE TB-VALOR TO TX100O
                             WHEN 11
                                MOVE TB-VALOR TO TX110O
                          END-EVALUATE
                       END-IF
                   END-PERFORM
                   PERFORM 20200-CLOSE-CURSOR-MAPS
      *
                   EXEC CICS
                       SEND MAP    ('HOLMMP')
                            MAPSET ('HOLMMP')
                            FROM   (HOLMMPO)
                            ERASE
                   END-EXEC
      *
      *--- Envio del mapa Solo datos
      *
               WHEN ENVIO-DATAONLY
                   EXEC CICS
                       SEND MAP     ('HOLMMP')
                            MAPSET  ('HOLMMP')
                            FROM    (HOLMMPO)
                            DATAONLY
                   END-EXEC
      *
           END-EVALUATE.
      *
       8000-ENVIO-MAPA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Envia un mensaje al terminal del usuario (fin sesion)    ---
      *----------------------------------------------------------------
       8100-ENVIA-MENSAJE.
      *
      *--- TXMUL ->
           MOVE 'HOLMEND'                  TO CL-CLAVE1.
           PERFORM 20100-OBTENER-TEXTO.
           MOVE CL-VALOR                   TO WL-FIN-SESION.
      * 'La transaccion HOLM ha finalizado...'
      *--- TXMUL <-
           EXEC CICS
               SEND TEXT FROM(WL-FIN-SESION)
                         ERASE
                         FREEKB
           END-EXEC.
      *
       8100-ENVIA-MENSAJE-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Devuelve el control a una transaccion                    ---
      *----------------------------------------------------------------
       8200-RETORNO-TRANS.
           EXEC CICS
               RETURN
               TRANSID      (WA-TRANSACCION)
               COMMAREA     (CH-COMMAREA-HOL)
           END-EXEC.
      *
       8200-RETORNO-TRANS-EXIT.
           EXIT.
      *--- TXMUL ->
      *----------------------------------------------------------------
      *--- Recibe el mapa de la tarea                               ---
      *----------------------------------------------------------------
       8400-RECIBE-MAPA.
      *
           EXEC CICS
                RECEIVE
                MAP     ('HOLMMP')
                MAPSET  ('HOLMMP')
                INTO    (HOLMMPI)
           END-EXEC.
      *
       8400-RECIBE-MAPA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Llama a otro programa CICS por LINK con Commarea         ---
      *----------------------------------------------------------------
       8500-LINK-PROGRAMA.
      *
           EXEC CICS
                LINK
                PROGRAM (CH-LINK)
                COMMAREA(CH-COMMAREA-HOL)
                LENGTH  (LENGTH OF CH-COMMAREA-HOL)
           END-EXEC.
      *
       8500-LINK-PROGRAMA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Lanza una transacci¢n por START                          ---
      *----------------------------------------------------------------
       8600-START-TRANSACCION.
      *
           EXEC CICS
                START
                TRANSID   (CH-TRANSACCION)
                FROM      (CH-COMMAREA-HOL)
                LENGTH    (LENGTH OF CH-COMMAREA-HOL)
           END-EXEC.
      *
       8600-START-TRANSACCION-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Lanza una transacci¢n por START SIN COMMAREA             ---
      *----------------------------------------------------------------
       8610-START-NO-COM.
      *
           EXEC CICS
                START
                TRANSID   (CH-TRANSACCION)
           END-EXEC.
      *
       8610-START-NO-COM-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Cede el control a otro programa CICS                     ---
      *----------------------------------------------------------------
       8700-XCTL-PROGRAMA.
      *
           EXEC CICS
                SET TERMINAL(EIBTRMID) UCTRAN
           END-EXEC
      *
           EXEC CICS
               XCTL PROGRAM  (CH-XCTL)
                    COMMAREA (CH-COMMAREA-HOL)
                    RESP     (WA-RESPUESTA-CICS)
           END-EXEC.
      *
       8700-XCTL-PROGRAMA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- CEDF visualiza una traza - Quitar para produccion        ---
      *----------------------------------------------------------------
       8800-TRAZA.
      *
           EXEC CICS
               ENTER       TRACEID(0)
               FROM        (WA-TRAZA)
           END-EXEC.
      *
       8800-TRAZA-EXIT.
           EXIT.
      *----------------------------------------------------------------
      *--- Devuelve el control a CICS - Salida total                ---
      *----------------------------------------------------------------
       8900-RETORNO-CICS.
      *
           EXEC CICS
                SET TERMINAL(EIBTRMID) UCTRAN
           END-EXEC
      *
           EXEC CICS
                RETURN
           END-EXEC.
      *
       8900-RETORNO-CICS-EXIT.
           EXIT.
      *
      * Rutinas multiidioma
      *
           EXEC SQL
                INCLUDE PTXMUL
           END-EXEC.
      *
       999999-DB2-ERRORES SECTION.
      *----------------------------------------------------------------
      *--- DB2 Errores y otros                                      ---
      *----------------------------------------------------------------
           EXEC SQL
                INCLUDE PDDB2
           END-EXEC.
      *----------------------------------------------------------------
      *-------------------- FIN DE PROGRAMA  --------------------------
      *----------------------------------------------------------------
